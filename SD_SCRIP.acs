#include "zcommon.acs"
#define CSF_SOLIDGROUND 1
#define CSF_SOLIDACTORS 2
#define CSF_ALL (CSF_SOLIDGROUND|CSF_SOLIDACTORS)

script "Taunt" (void)
{
	switch(PlayerClass(PlayerNumber()))
      {
            case 0:
                 AmbientSound("sonicdoom/sonictaunt", 127);
                 break;
            case 1:
                 AmbientSound("sonicdoom/amytaunt", 127);
                 break;
            case 2:
                 AmbientSound("sonicdoom/classictaunt", 127);
                 break;
            case 3:
                 AmbientSound("sonicdoom/shadowtaunt", 127);
                 break;
            case -1:
            default:
                 break;
        }
}

script "HammerJump" (void)
{
	int buttons = GetPlayerInput(-1, INPUT_BUTTONS);

	if (buttons & BT_CROUCH)
	{
		ThrustThingZ(0, 70, 0, 1);
        AmbientSound("*jump", 127);
		SetResultValue(1);
	}
	else
	{
		SetResultValue(0);
	}
}

script 3 (void) // I have no clue what this is supposed to do
{
    ThrustThingZ(0, 7, 0, 1);
}

script "TransformSuper" (void)
{
	if (CheckInventory("PowerSuper"))
	{
		switch(PlayerClass(PlayerNumber()))
		{
            case 0: // Sonic
                 if (GetCvar("VocalTracks"))
				 {
					 SetMusic("v_supsnc", 0);
				 }
				 else
				 {
					 SetMusic("m_supsnc", 0);
				 }
                 break;
            case 1: // Amy
                 SetMusic("m_supamy", 0);
                 break;
            case 2: // Classic Sonic
                 if (GetCvar("VocalTracks"))
				 {
					 SetMusic("v_supcls", 0);
				 }
				 else
				 {
					 SetMusic("m_supcls", 0);
				 }
                 break;
            case 3: // Shadow
                 SetMusic("m_supsha", 0);
                 break;
            case -1:
            default:
                 break;
        }
		while (CheckInventory("PowerSuper"))
		{
		    SetMugShotState("Super");
			Delay(35);
		}
		SetMusic("*", 0);
		SetMugShotState("Normal");
	}
	else
	{
		SetMusic("*", 0);
	}
}

script "TransformHyper" (void)
{
	if (CheckInventory("PowerHyper"))
	{
		while (CheckInventory("PowerHyper"))
		{
			SetMusic("m_hypcls", 0);
		    SetMugShotState("Hyper");
			TakeInventory("PowerSuper", 1);
			TakeInventory("ChaosEmeralds", 1);
			Delay(35); 
		}
		SetMusic("*", 0);
		SetMugShotState("Normal");
	}
	else
	{
		SetMusic("*", 0);
	}
}

script "Invincibility" (void)
{
	if (!CheckInventory("PowerSuper") && !CheckInventory("PowerHyper"))
	{
		if (CheckInventory("PowerInvulnerable"))
		{
			switch(PlayerClass(PlayerNumber()))
			{
				case 0: 
					SetMusic("s_invinc", 0);
					break;
				case 1: 
					SetMusic("a_invinc", 0);
					break;
				case 2: 
					SetMusic("c_invinc", 0);
					break;
				case 3:
					SetMusic("s_invinc", 0);
					break;
				case -1:
				default:
					break;
			}
			while (CheckInventory("PowerInvulnerable"))
			{
				delay(15);
			}
			SetMusic("*", 0);
		}
		else
		{
			SetMusic("*", 0);
		}
	}
}

script "ResetMusic" ENTER
{
	SetMugShotState("Normal");
	SetMusic("*", 0);
}

script "ChaosEmeraldHandler" (void)
{
	if (CheckInventory("PowerSuper") || CheckInventory("PowerHyper"))
	{
		if (PlayerClass(PlayerNumber()) == 2 && !CheckInventory("PowerHyper"))
		/* If playing as Classic Sonic and you're not already in Hyper form... */
		{
			Log(s:"Hyper Sonic!!");
			TakeInventory("PowerSuper", 1);
			GiveInventory("HyperForm", 1);
			ACS_NamedExecute("TransformHyper", 0);
			ACS_NamedExecute("TakeRingsIfHyper", 0);
		}
		else
		{
			Log(s:"Chaos Energy refilled!");
			TakeInventory("ChaosEmeralds", 1);
			GiveInventory("ChaosEnergy", 999);
		}
	}
	else
	{
		switch(PlayerClass(PlayerNumber()))
		{
			case 1:
				Log(s:"\clPress \cf", k:"invuse", s:" \clto activate Dark form!");
				break;
			case -1:
				Log(s:"\clPress \cf", k:"invuse", s:" \clto activate Super form!");
				break;
			default:
				Log(s:"\clPress \cf", k:"invuse", s:" \clto activate Super form!");
				break;
		}
	}
}

script "NoBrutal4U" ENTER
{
	if (CheckClass("BrutalWeapon"))
	{
		if (GetLevelInfo(LEVELINFO_CLUSTERNUM) != 412)
		{
			ChangeLevel("ERR666", 0, CHANGELEVEL_NOINTERMISSION);
		}
	}
	else
	{
		if (GetLevelInfo(LEVELINFO_CLUSTERNUM) == 412)
		{
			switch (Random(1, 6))
			{
				case 1:
					Print(s:"See you next game!");
					break;
				case 2:
					Print(s:"You aren't meant to be here");
					break;
				case 3:
					Print(s:"Sorry, this level is only for heretics");
					break;
				case 4:
					Print(s:"No! This isn't the way you're supposed to play the game!");
					break;
				case 5:
					Print(s:"lol no");
					break;
				case 6:
					Print(s:"Why would you even...?");
					break;
				default:
					Print(s:"Send chicken wings");
					break;
			}
			Exit_Normal(0);
		}
	}
}

script "DeathMusic" (void)
{
	switch(PlayerClass(PlayerNumber()))
	{
		case 0:
			break;
		case 1:
			break;
		case 2:
			SetMusic("noway", 0);
			break;
		case 3:
			SetMusic("allfail", 0);
			break;
		case -1:
		default:
			break;
	}
}

script "TakeRingsIfSuper" (void)
{
	while (CheckInventory("PowerSuper"))
	{
		Delay(2);
		TakeInventory("ChaosEnergy", 1);
		if (!CheckInventory("ChaosEnergy"))
		{
			TakeInventory("PowerSuper", 1);
			TakeInventory("PowerInvulnerable", 1);
		}
	}
}

script "TakeRingsIfHyper" (void)
{
	while (CheckInventory("PowerHyper"))
	{
		Delay(2);
		TakeInventory("ChaosEnergy", 2);
		if (!CheckInventory("ChaosEnergy"))
		{
			TakeInventory("PowerHyper", 1);
			TakeInventory("PowerInvulnerable", 1);
		}
	}
}

script "SetAirControl" ENTER
{
	switch(PlayerClass(PlayerNumber()))
	{
		case 0:
            SetAirControl(0.5);
            break;
        case 1:
            SetAirControl(1.0);
            break;
        case 2:
            SetAirControl(0.35);
            break;
        case 3:
            SetAirControl(0.5);
            break;
        case -1:
        default:
            break;
    }
}

script "KickCooldown" (void)
{
	while (CheckInventory("KickCooldown"))
	{
		TakeInventory("KickCooldown", 1);
		Delay(1);
	}
}