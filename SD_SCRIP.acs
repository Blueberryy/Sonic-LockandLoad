#library "snclkld"
#include "zcommon.acs"
#define Sonic 0
#define Amy 1
#define ClassicSonic 2
#define Shadow 3


script "Taunt" (void)
{
	NoiseAlert(0, 0);
	switch(PlayerClass(PlayerNumber())) {
		case Sonic:
			PlaySound(1001, "sonic/taunt", 9, 1.0);
			SetMugshotState("GrinSmol");
            break;
		case Amy:
			PlaySound(1001, "amy/taunt", 9, 1.0);
			SetMugshotState("FailSmol");
			break;
		case ClassicSonic:
			int SandInEye = random(0, 1);
			if (SandInEye) {
				PlaySound(1001, "classic/taunt5", 9, 1.0);
				SetMugshotState("FailSmol");
			} else {
				PlaySound(1001, "classic/taunt", 9, 1.0);
				SetMugshotState("GrinSmol");
			}
			break;
		case Shadow:
			if (random(1, 35) == 35) {
				PlaySound(1001, "imthe/coolest", 9, 1.0);
				SetMugshotState("Grin");
			} else {
				PlaySound(1001, "shadow/taunt", 9, 1.0);
			}
			break;
		case -1:
			default:
			break;
	}
}

script "IsCrouching" (void)
{
	int buttons = GetPlayerInput(-1, INPUT_BUTTONS);

	if (buttons & BT_CROUCH) { SetResultValue(1); }
	else { SetResultValue(0); }
}

script 3 (void) // I have no clue what this is supposed to do
{
    ThrustThingZ(0, 7, 0, 1);
}

script "TransformSuper" (void)
{
	if (CheckInventory("PowerSuper"))
	{
		switch(PlayerClass(PlayerNumber()))
		{
            case Sonic: // Super Sonic
                 if (GetCvar("SupSonic") == 1) {
					 SetMusic("S_SUPER", 0);
				 }
				 else if (GetCvar("SupSonic") == 2) {
					 SetMusic("S_SUPER2", 0);
				 }
                 break;
            case Amy: // Dark Amy
                 if (GetCvar("SupAmy")) {
					SetMusic("A_SUPER", 0);
				 }
                 break;
            case ClassicSonic: // Classic Super Sonic
                 if (GetCvar("SupClassic") == 1) {
					 SetMusic("C_SUPER", 0);
				 }
				 else if (GetCvar("SupClassic") == 2) {
					 SetMusic("C_SUPER2", 0);
				 }
                 break;
            case Shadow: // Super Shadow
                 if (GetCvar("SupShadow") == 1) {
					 SetMusic("D_SUPER", 0);
				 }
				 else if (GetCvar("SupShadow") == 2) {
					 SetMusic("D_SUPER2", 0);
				 }
                 break;
            case -1:
            default:
                 break;
        }
		while (CheckInventory("PowerSuper"))
		{
		    SetMugShotState("Super");
			Delay(35);
		}
		if (GetCvar("SupSonic")||GetCvar("SupAmy")||GetCvar("SupClassic")||GetCvar("SupShadow")) { 
			SetMusic("*", 0);
		}
		SetMugShotState("Normal");
	}
	else
	{
		SetMusic("*", 0);
	}
}

script "TransformHyper" (void)
{
	if (CheckInventory("PowerHyper"))
	{
		while (CheckInventory("PowerHyper"))
		{
			if (GetCvar("HyperMusic")) {
				switch(PlayerClass(PlayerNumber())) {
					case Sonic:
						SetMusic("S_HYPER", 0);
						break;
					case ClassicSonic:
						SetMusic("C_HYPER", 0);
						break;
					default:
						 break;
				}
			}
		    SetMugShotState("Hyper");
			TakeInventory("PowerSuper", 1);
			TakeInventory("ChaosEmeralds", 1);
			Delay(35); 
		}
		if (GetCvar("HyperMusic")) { SetMusic("*", 0); }
		SetMugShotState("Normal");
		SetActorProperty(1001, APROP_JumpZ, 12.0);
	}
	else
	{
		if (GetCvar("HyperMusic")) { SetMusic("*", 0); }
		SetActorProperty(1001, APROP_JumpZ, 12.0);
	}
}

script "Invincibility" (void)
{
	if (!CheckInventory("PowerSuper") && !CheckInventory("PowerHyper"))
	{
		if (CheckInventory("PowerInvulnerable"))
		{
			switch(PlayerClass(PlayerNumber()))
			{
					case 0: 
						if (GetCvar("InvSonic")) { SetMusic("s_invinc", 0); }
						break;
					case 1: 
						if (GetCvar("InvAmy")) { SetMusic("a_invinc", 0); }
						break;
					case 2: 
						if (GetCvar("InvClassic")) { SetMusic("c_invinc", 0); }
						break;
					case 3:
						if (GetCvar("InvShadow")) { SetMusic("d_invinc", 0); }
						break;
					case -1:
					default:
						break;
			}
			while (CheckInventory("PowerInvulnerable"))
			{
				delay(15);
			}
			if (GetCvar("InvSonic")||GetCvar("InvAmy")||GetCvar("InvClassic")||GetCvar("InvShadow")) { 
				SetMusic("*", 0);
			}
		}
		else
		{
			if (GetCvar("InvSonic")||GetCvar("InvAmy")||GetCvar("InvClassic")||GetCvar("InvShadow")) { 
				SetMusic("*", 0);
			}
		}
	}
}

script "InitMusic" ENTER
{
	SetMugShotState("Normal");
	SetMusic("*", 0);
}

script "ChaosEmeraldHandler" (void)
{
	int character = PlayerClass(PlayerNumber());
	if (CheckInventory("PowerSuper") || CheckInventory("PowerHyper"))
	{
		if (character == Sonic || character == ClassicSonic && !CheckInventory("PowerHyper") && GetCvar("HyperSonic"))
		{
			switch (PlayerClass(PlayerNumber())) {
				case Sonic:
					PlaySound(1001, "super/sonich", 6, 1.0);
					Log(s:"\c[Sonic]Now I'm serious!!");
					break;
				case ClassicSonic:
					PlaySound(1001, "super/classich", 6, 1.0);
					Log(s:"\c[Classic]Okay, NO more playing around!!");
					break;
				default:
					break;
			}
			TakeInventory("PowerSuper", 1);
			GiveInventory("HyperForm", 1);
			ACS_NamedExecute("TransformHyper", 0);
			ACS_NamedExecute("TakeRingsIfHyper", 0);
		}
		else
		{
			Log(s:"Chaos Energy refilled!");
			TakeInventory("ChaosEmeralds", 1);
			GiveInventory("ChaosEnergy", 999);
		}
	}
	else
	{
		switch(PlayerClass(PlayerNumber()))
		{
			case 1:
				Log(s:"\c[Purple]Press \cl", k:"invuse", s:" \c[Purple]to embrace the darkness!"); // Amy
				break;
			case 3:
				Log(s:"\c[Gold]Press \cl", k:"invuse", s:" \c[Gold]to activate Super Shadow!"); // Shadow
				break;
			default:
				Log(s:"\c[Gold]Press \cl", k:"invuse", s:" \c[Gold]to activate Super Sonic!"); // Sonic and Classic
				break;
		}
	}
}

script "SuperTransformPrompt" enter
{
	while (1) {
		Delay(1);
		int x = 0.5;
		int y;
		switch (GetCvar("HUDType")) {
			case 3:
				// Legacy
				y = 0.8;
				break;
			default:
				// Unified, Classic-Style
				y = 0.9;
				break;
		}
		if (CheckInventory("ChaosEmeralds")) {
			switch(PlayerClass(PlayerNumber()))
			{
				case Sonic:
					HudMessage(s:"\c[Gold]", k:"invuse", s:"  \c[Sonic]Super Sonic";
					HUDMSG_PLAIN, 1, CR_UNTRANSLATED, x, y, 0);
					break;
				case Amy:
					HudMessage(s:"\c[Purple]", k:"invuse", s:"  \c[Amy]Dark Amy";
					HUDMSG_PLAIN, 1, CR_UNTRANSLATED, x, y, 0);
					break;
				case ClassicSonic:
					HudMessage(s:"\c[Gold]", k:"invuse", s:"  \c[Classic]Super Sonic";
					HUDMSG_PLAIN, 1, CR_UNTRANSLATED, x, y, 0);
					break;
				case Shadow:
					HudMessage(s:"\c[Gold]", k:"invuse", s:"  \c[Shadow]Super Shadow";
					HUDMSG_PLAIN, 1, CR_UNTRANSLATED, x, y, 0);
					break;
				default:
					break;
			}
		}
		else { HudMessage(s:""; HUDMSG_PLAIN, 1, CR_UNTRANSLATED, 0, 0, 0, 0); }
	}
}

script "DeathMusic" (int xdeath)
{
	if (GetCvar("DeathMusic")) {
		if (xdeath) {
			switch(PlayerClass(PlayerNumber())) {
				case ClassicSonic:
					SetMusic("C_DEADX", 0);
					break;
				case Shadow:
					SetMusic("D_DEADX", 0);
					break;
				default:
					break;
			}
		} 
		
		else {
			switch(PlayerClass(PlayerNumber()))
			{
				case Sonic:
					SetMusic("S_DEADM", 0);
					break;
				case Amy:
					SetMusic("A_DEADM", 0);
					break;
				case ClassicSonic:
					SetMusic("C_DEADM", 0);
					Delay(9*35);
					SetMusic("*");
					break;
				case Shadow:
					SetMusic("D_DEADM", 0);
					Delay(7*35);
					SetMusic("*");
					break;
				default:
					break;
			}
		}
	}
}

script "TakeRingsIfSuper" (void)
{
	while (CheckInventory("PowerSuper"))
	{
		switch(PlayerClass(PlayerNumber()))
		{
			case Amy:
				TakeInventory("ChaosEnergy", 2);
				break;
			default:
				TakeInventory("ChaosEnergy", 1);
				break;
		}
		Delay(2);
		if (!CheckInventory("ChaosEnergy"))
		{
			TakeInventory("PowerSuper", 1);
			TakeInventory("PowerInvulnerable", 1);
		}
	}
}

script "TakeRingsIfHyper" (void)
{
	while (CheckInventory("PowerHyper"))
	{
		Delay(2);
		TakeInventory("ChaosEnergy", 2);
		if (!CheckInventory("ChaosEnergy"))
		{
			TakeInventory("PowerHyper", 1);
			TakeInventory("PowerInvulnerable", 1);
		}
	}
}

script "SetAirControl" ENTER
{
	SetAirControl(0.35);
}

script "KickCooldown" ENTER
{
	while (true)
	{
		Delay(1);
		TakeInventory("KickCooldown", 1);
	}
}

script "RingCountCheck" ENTER
{
	while (true)
	{
		Delay(2);
		if (CheckInventory("Health") > 500)
		{
			GiveInventory("OverRinged2", 1);
		}
		else if (CheckInventory("Health") > 300)
		{
			GiveInventory("OverRinged", 1);
		}
		else
		{
			TakeInventory("PowerOverRinged", 1);
			TakeInventory("PowerOverRinged2", 1);
		}
	}
	
}

script "GiveTID" ENTER
{
	Thing_ChangeTID(0, 1001);
}

script "Damn4thChaosEmerald" (void)
{
	AmbientSound("shadow/damn4thchaos", 127);
}

script "NoMusic" (void)
{
	SetMusic("", 0);
}

script "ResetMusic" (void)
{
	SetMusic("*", 0);
}

script "ReticleSound" (void)
{
	AmbientSound("sweapons/reticle", 127);
}

/*
script "YouGotExe-cuted" DEATH
{
	if (ThingCount(T_BRAINS, 0))
	{
		ChangeLevel("CONTINUE", 0, CHANGELEVEL_NOINTERMISSION, -1);
	}
}
*/

script "ContinuingSucks" (void)
{
	ClearInventory();
	SetActorProperty(1001, APROP_Health, 1);
}

function void SetMugshotIfNotSuper(str state)
{
	if (!CheckInventory("PowerSuper") && !CheckInventory("PowerHyper")) {
		SetMugShotState(state);
	}
}

script "GoalRing" (void)
{
	Thing_Stop(1001);
	GiveInventory("Invincibility", 1);
	int LevelTime = Timer() / 35;
	int ParTime = GetLevelInfo(LEVELINFO_PAR_TIME);
	int DParTime = ParTime * 2;
	int CurrentHealth = GetActorProperty(1001, APROP_Health);
	int KilledMonsters = GetLevelInfo(LEVELINFO_KILLED_MONSTERS);
	int TotalMonsters = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS);
	SetFont("BIGFONT");
	switch (PlayerClass(PlayerNumber()))
	{
		case Sonic:
			SetMusic("S_VICTOR");
			Print(s:"\c[Sonic]Stage Clear");
			if (LevelTime > DParTime) {
				PlaySound(1001, "sonic/badresult");
				SetMugshotIfNotSuper("Failure");
			}
			else if (LevelTime > ParTime) {
				PlaySound(1001, "sonic/okayresult");
				SetMugshotIfNotSuper("Failure");
			}
			else {
				PlaySound(1001, "sonic/goodresult");
				SetMugshotIfNotSuper("Grin");
			}
			Delay(35*7);
			break;
		case Amy:
			SetMusic("A_VICTOR");
			Print(s:"\c[Amy]Act Clear!");
			if (CurrentHealth < 40) {
				PlaySound(1001, "amy/badresult");
				SetMugshotIfNotSuper("Failure");
			}
			else {
				PlaySound(1001, "amy/goodresult", CHAN_BODY, 0.7);
				SetMugshotIfNotSuper("Grin");
			}
			Delay(35*7);
			break;
		case ClassicSonic:
			SetMusic("C_VICTOR");
			if (CheckInventory("PowerHyper")) {
				Print(s:"\c[Purple]HYPER SONIC \c[White]GOT THROUGH");
			} else if (CheckInventory("PowerSuper")) {
				Print(s:"\c[Gold]SUPER SONIC \c[White]GOT THROUGH");
			} else {
				Print(s:"\c[Classic]SONIC \c[White]GOT THROUGH");
			}
			if (CurrentHealth < 21) {
				PlaySound(1001, "classic/badresult");
				SetMugshotIfNotSuper("Failure");
			}
			else if (LevelTime > ParTime) {
				PlaySound(1001, "classic/okayresult");
				SetMugshotIfNotSuper("OK");
			}
			else {
				PlaySound(1001, "classic/goodresult");
				SetMugshotIfNotSuper("Grin");
			}
			Delay(35*6);
			break;
		case Shadow:
			SetMusic("D_VICTOR");
			if (KilledMonsters == TotalMonsters) {
				Print(s:"\c[Shadow]Mission Cleared");
				PlaySound(1001, "shadow/goodresult");
				SetMugshotIfNotSuper("OK");
			}
			else {
				Print(s:"\c[Shadow]Mission Incomplete");
				PlaySound(1001, "shadow/badresult");
				SetMugshotIfNotSuper("Failure");
			}
			Delay(35*7);
			break;
		case -1:
		default:
			break;
	}
	Exit_Normal(0);
}

script "SonicN64" (void)
{
	AmbientSound("sonic/nintendo64", 127);
}

script "Drowning" ENTER
{
	int drowntimer;
	int maxdrown;
	while (1) {
		if (GetActorProperty(1001, APROP_Waterlevel) == 3) {
			if (!CheckActorInventory(1001, "PowerSuper") && !CheckActorInventory(1001, "PowerHyper") && !CheckActorInventory(1001, "PowerInvulnerable")) {
				drowntimer++;
			}
			if (drowntimer > 4)
			{
				switch(PlayerClass(PlayerNumber())) {
					case 0:
						SetMusic("S_DROWN", 0);
						break;
					case 1:
						SetMusic("A_DROWN", 0);
						break;
					case 2:
						SetMusic("C_DROWN", 0);
						break;
					case 3:
						SetMusic("D_DROWN", 0);
						break;
					default:
						break;
				}
			}
			if (drowntimer > 16)
			{
				DamageActor(1001, AAPTR_PLAYER1, 0, 0, 9999, "Drownd");
				if (!GetCvar("DeathMusic")) { SetMusic("*"); }
				break;
			}
		}
		else {
			if (drowntimer > 4) {
				SetMusic("*");
			}
			drowntimer = 0;
		}
		Delay(35);
	}
}

script "ReactionTime" (void)
{
	AmbientSound("sglobal/reaction", 127);
}

script "BossMusic2" (void)
{
	SetMusic("B_SCRT2");
}

script "JumpHeight" (int Reset)
{
	if (Reset) {
		switch(PlayerClass(PlayerNumber())) {
			case Shadow:
				SetActorProperty(1001, APROP_JumpZ, 17.0);
				break;
			default:
				SetActorProperty(1001, APROP_JumpZ, 15.0);
				break;
		}
	} else {
		switch(PlayerClass(PlayerNumber())) {
            case Sonic:
				if (CheckInventory("PowerHyper")) {
					// Hyper Sonic
					SetActorProperty(1001, APROP_JumpZ, 20.0);
				} else {
					// Super Sonic
					SetActorProperty(1001, APROP_JumpZ, 17.0);
				}
                break;
            case Amy: // Dark Amy
				SetActorProperty(1001, APROP_JumpZ, 17.0);
                break;
            case ClassicSonic:
				if (CheckInventory("PowerHyper")) {
					// Classic Hyper Sonic
					SetActorProperty(1001, APROP_JumpZ, 20.0);
				} else {
					// Classic Super Sonic
					SetActorProperty(1001, APROP_JumpZ, 17.0);
				}
                break;
            case Shadow: // Shadow
				SetActorProperty(1001, APROP_JumpZ, 21.0);
                break;
            case -1:
            default:
                 break;
        }
	}
}

script "DoubleJump" enter
{
	/*
	 * Thanks to Agent ME on the ZDoom Forum for this script
	 * <https://forum.zdoom.org/viewtopic.php?t=21034>
	 */
	int playerCharacter = PlayerClass(PlayerNumber());
	
	int maxDblJumps = 1;
	int dblJumpThrust = 60;

	int lastZ = GetActorZ(0);
	int olderZ;
	int counter = 0;
	while (true)
	{
		olderZ = lastZ;
		lastZ = GetActorZ(0);
		delay(1);
		
		if (playerCharacter == Sonic && CheckInventory("PowerHyper")) {
			maxDblJumps = 2;
		} else {
			maxDblJumps = 1;
		}

		if ((GetPlayerInput(-1, MODINPUT_BUTTONS) & BT_JUMP) && !(GetPlayerInput(-1, MODINPUT_OLDBUTTONS) & BT_JUMP) && (playerCharacter == Sonic || playerCharacter == Amy || (playerCharacter == ClassicSonic && CheckInventory("PowerHyper"))) && GetCvar("DoubleJump") && (ClassifyActor(1001) & ACTOR_ALIVE)) {
			if ((GetActorZ(0) > lastZ) && (lastZ <= olderZ)) {
				counter = 0;
			} else {
				if (counter < maxDblJumps) {
					if (GetCvar("DoubleJump") == 1) { Thing_Stop(1001); }
					ThrustThingZ(0, dblJumpThrust, 0, 0);
					switch (playerCharacter) {
						case Sonic:
							PlaySound(1001, "sonic/doublejump", CHAN_BODY);
							break;
						case Amy:
							PlaySound(1001, "amy/doublejump", CHAN_BODY);
							break;
						case ClassicSonic:
							PlaySound(1001, "classic/jump", CHAN_BODY);
							break;
						default:
							break;
					}
					counter++;
				}
			}
		} else if ((olderZ == lastZ) && (lastZ == GetActorZ(0))) {
			counter = 0;
		}
	}
}