/* Visit sll-base.acs for information */

script "StyleRank" ENTER
{
	int oldrank;
	str callout;
	int oldcalloutrank;

	int stylerank_x;
	int stylename_x;
	int stylescore_x;

	int stylerank_y;
	int stylename_y;
	int stylescore_y;

	SetCvar("StyleRank", 0);
	Delay(1);
	while (GetCvar("StyleSystem")) {
		int stylerank = GetCvar("StyleRank");
		if      (stylerank == SSS) SetFont("sss_rank");
		else if (stylerank == SS) SetFont("ss_rank");
		else if (stylerank == S) SetFont("s_rank");
		else if (stylerank == A) SetFont("a_rank");
		else if (stylerank == B) SetFont("b_rank");
		else if (stylerank == C) SetFont("c_rank");
		else if (stylerank == D) SetFont("d_rank");

		str StyleName;
		str StyleColour;
		switch (GetCvar("StyleRank")) {
			case SSS:
				StyleName = "stylerank_sss"; // SSS
				StyleColour = "\c[S]";
				break;
			case SS:
				StyleName = "stylerank_ss";  // SS
				StyleColour = "\c[S]";
				break;
			case S:
				StyleName = "stylerank_s"; 	 // S
				StyleColour = "\c[S]";
				break;
			case A:
				StyleName = "stylerank_a";	 // A
				StyleColour = "\c[A]";
				break;
			case B:
				StyleName = "stylerank_b";	 // B
				StyleColour = "\c[B]";
				break;
			case C:
				StyleName = "stylerank_c";	 // C
				StyleColour = "\c[C]";
				break;
			case D:
				StyleName = "stylerank_d";	 // D
				StyleColour = "\c[D]";
				break;
			default:
				StyleName = "";              // E
				break;
		}

        stylerank_x  = 1.5;
        stylename_x  = 1.5;
        stylescore_x = 1.5;

        stylerank_y  = 0.16;
        stylename_y  = 0.24;
        stylescore_y = 0.28;

		if (stylerank > 0 && ClassifyActor(0) & ACTOR_ALIVE) {
			HudMessage(s:"A"; HUDMSG_PLAIN, 43, CR_UNTRANSLATED, stylerank_x, stylerank_y, 0.1, 1.0);
			if (stylerank > oldrank && stylerank > oldcalloutrank && GetCvar("StyleCallouts")) {
				if      (stylerank == SSS) callout = "sstyle/supersonic";
				else if (stylerank == SS)  callout = "sstyle/amazing";
				else if (stylerank == S)   callout = "sstyle/outstanding";
				else if (stylerank == A)   callout = "sstyle/awesome";
				else if (stylerank == B)   callout = "sstyle/great";
				else if (stylerank == C)   callout = "sstyle/good";
				else if (stylerank == D)   callout = "sstyle/dontgiveup";
				oldcalloutrank = stylerank;
				PlaySound(0, callout, 4096, 1.0, false, ATTN_NONE, false);
			}
		}

		if (stylerank < oldcalloutrank - 1 || stylerank < D) {
			oldcalloutrank = 0;
		}

		if (StyleName != "") {
			SetFont("SMALLFONT");
			HudMessage(s:StyleColour, l:StyleName; HUDMSG_PLAIN, 42, CR_WHITE, stylename_x, stylename_y, 0.1, 1.0);
		}

		oldrank = GetCvar("StyleRank");

		if (stylerank >= 1 && GetCvar("StylePoints")) {
			SetFont("SMALLFONT");
			HudMessage(d:GetCvar("StyleScore"); HUDMSG_PLAIN, 41, CR_WHITE, stylescore_x, stylescore_y, 0.1, 1.0);
		}

		Delay(1);
	}
}