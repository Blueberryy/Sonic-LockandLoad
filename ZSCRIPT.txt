version "4.0"

Class SonicKick : CustomInventory
{
	int inuse;
	int EXKick;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "TimeCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	TimeCheck:
		TNT1 A 0 A_JumpIf(CountInv("KickCooldown")<1, "FloorCheck");
		Stop;
	FloorCheck:
		TNT1 A 0
		{
			invoker.EXKick = 0;
		}
	    TNT1 A 0 A_CheckFloor("KickAttack");
		TNT1 A 0
		{
			invoker.EXKick = 1;
		}
	KickAttack:
		TNT1 A 0 A_JumpIfInventory("PowerSuper", 1, "SuperKickAttack");
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_PlaySound("sonicdoom/grunt", 8);
		TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SonicEagle");
		KICK A 1 A_PlaySound("sonicdoom/swordswing", 7);
		KICK BC 1;
		KICK D 1 A_CustomPunch(random(1,16)*24, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
		KICK EF 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SonicEagle:
		EAGL A 1 A_PlaySound("sonicdoom/swordswing", 7);
		EAGL BC 1;
		EAGL D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
		EAGL E 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SuperKickAttack:
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_PlaySound("sonicdoom/grunt", 8);
		TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SuperSonicEagle");
		SKIK A 1 A_PlaySound("sonicdoom/swordswing", 7);
		SKIK BC 1;
		SKIK D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
		SKIK EF 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SuperSonicEagle:
		SEAG A 1 A_PlaySound("sonicdoom/swordswing", 7);
		SEAG BC 1;
		SEAG D 1 A_CustomPunch(random(1,16)*96, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
		SEAG E 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 5;
		}
	}
}

Class ShadowDash : CustomInventory
{
	int inuse;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "DashCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	DashCheck:
		TNT1 A 0 A_JumpIf(CountInv("TeleportCooldown")<1, "Dash");
		Stop;
	Dash:
		TNT1 A 0 A_Stop();
		TNT1 A 0 A_SetGravity(0);
		TNT1 A 4;
		TNT1 A 0 A_SetGravity(1);
		TNT1 A 0 ThrustThing(angle * 256 / 360, 150, 1, 0);
		TNT1 A 0 A_PlaySound("sonicdoom/chaosspear");
		TNT1 A 4;
		TNT1 A 0 A_GiveInventory("TeleportCooldown", 50);
		TNT1 A 0 A_Stop();
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 8;
		}
	}
}

Class AmyKick : CustomInventory
{
	int inuse;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "TimeCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	TimeCheck:
		TNT1 A 0 A_JumpIf(CountInv("KickCooldown")<1, "KickAttack");
		Stop;
	KickAttack:
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		AKIK A 1 A_PlaySound("sonicdoom/swordswing", 7);
		TNT1 A 0 A_GiveInventory("LegOut", 1);
		AKIK BC 1;
		AKIK D 1 A_CustomPunch(10, false, CPF_NOTURN, "AmyKickPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
		AKIK EFG 1;
		TNT1 A 0 A_TakeInventory("LegOut", 1);
		TNT1 A 1 A_SetInventory("KickCooldown", 10);
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 6;
		}
	}
}

Class SonicKickPuff : BulletPuff
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		RenderStyle "None";
		Alpha 0;
		VSpeed 1;
		Mass 5;
		DamageType "SonicKick";
		ProjectileKickBack 1000;
	}
	States
	{
		Spawn:
			TNT1 A 0;
		Melee:
			TNT1 A 0;
			Stop;
	}
}

Class AmyKickPuff : BulletPuff
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		RenderStyle "None";
		Alpha 0;
		VSpeed 1;
		Mass 5;
		DamageType "SonicKick";
		ProjectileKickBack 2000;
	}
	States
	{
		Spawn:
			TNT1 A 0;
		Melee:
			TNT1 A 0;
			Stop;
	}
}

Class SonicPunch : CustomInventory
{
	int inuse;
	int EXKick;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "PunchTheChild");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	PunchTheChild:
		PUNG B 1;
		PUNG C 1
		{
			if (CountInv("PowerSuper") < 1)
			{
				A_CustomPunch(4, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
			}
			if (CountInv("PowerSuper") > 0)
			{
				A_CustomPunch(40, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
			}
			if (CountInv("PowerHyper") > 0)
			{
				A_CustomPunch(160, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
				A_FireProjectile("ChaosEnergyBallEX", 0, false);
			}
		}
		PUNG D 1;
		PUNG C 1;
		PUNG B 1;
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 5;
		}
	}
}

Class KickCooldown : Inventory
{
	Default
	{
		Inventory.MaxAmount 25;
	}
}