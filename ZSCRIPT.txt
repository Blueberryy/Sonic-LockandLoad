version "4.0"

Class SonicKick : CustomInventory
{
	int inuse;
	int EXKick;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "TimeCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	TimeCheck:
		TNT1 A 0 A_JumpIf(GetCvar("DkgBruTecFpgA"), "Errrod");
		TNT1 A 0 A_JumpIf(CountInv("KickCooldown")<1, "FloorCheck");
		Stop;
	FloorCheck:
		TNT1 A 0
		{
			invoker.EXKick = 0;
		}
	    TNT1 A 0 A_CheckFloor("KickAttack");
		TNT1 A 0
		{
			invoker.EXKick = 1;
		}
	KickAttack:
		TNT1 A 0 A_JumpIfInventory("PowerSuper", 1, "SuperKickAttack");
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_PlaySound("sonic/grunt", 8);
		TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SonicEagle");
		KICK A 1 A_PlaySound("sweapons/swordswing", 7);
		KICK BC 1;
		KICK D 1 A_CustomPunch(random(1,16)*24, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
		KICK EF 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SonicEagle:
		EAGL A 1 A_PlaySound("sweapons/swordswing", 7);
		EAGL BC 1;
		EAGL D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
		EAGL E 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SuperKickAttack:
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_PlaySound("sonic/grunt", 8);
		TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SuperSonicEagle");
		SKIK A 1 A_PlaySound("sweapons/swordswing", 7);
		SKIK BC 1;
		SKIK D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
		SKIK EF 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	SuperSonicEagle:
		SEAG A 1 A_PlaySound("sweapons/swordswing", 7);
		SEAG BC 1;
		SEAG D 1 A_CustomPunch(random(1,16)*96, true, CPF_NOTURN, "SonicKickPuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
		SEAG E 1;
		TNT1 A 1 A_SetInventory("KickCooldown", 5);
		Stop;
	Errrod:
		TNT1 A 0 A_Print("Error");
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 5;
		}
	}
}

Class AmyKick : CustomInventory
{
	int inuse;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "TimeCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	TimeCheck:
		TNT1 A 0 A_JumpIf(CountInv("KickCooldown")<1, "KickAttack");
		Stop;
	KickAttack:
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		AKIK A 1 A_PlaySound("sweapons/swordswing", 7);
		TNT1 A 0 A_GiveInventory("LegOut", 1);
		AKIK BC 1;
		AKIK D 1 A_CustomPunch(10, false, CPF_NOTURN, "AmyKickPuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
		AKIK EFG 1;
		TNT1 A 0 A_TakeInventory("LegOut", 1);
		TNT1 A 1 A_SetInventory("KickCooldown", 10);
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 6;
		}
	}
}

Class SonicDash : CustomInventory
{
	int inuse;
	States
	{
	Use:
		TNT1 A 0 A_Overlay(-2, "TimeCheck");
		TNT1 A 0 A_OverlayOffset(-2, 0, 32);
		Fail;
	TimeCheck:
		TNT1 A 0 A_JumpIf(CountInv("KickCooldown")<1, "DashAtk");
		Stop;
	DashAtk:
		TNT1 A 0 A_GiveInventory("KickCooldown", 25);
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_PlaySound("classic/dash", 2);
		TNT1 A 0 A_PlaySound("classic/homing", 3, 0.75);
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * 32, 0, Sin(Pitch) * -32, CVF_Relative);
		TNT1 A 5
		{
			if (CountInv("PowerSuper") < 1 && CountInv("PowerHyper") < 1)
			{
				A_CustomPunch(40, CPF_PULLIN | CPF_NOTURN, 0, "SonicDashPuff", 100, 0, 0, "ArmorBonus", "sweapons/finalhit");
			}
			else if (CountInv("PowerSuper"))
			{
				A_CustomPunch(160, CPF_PULLIN | CPF_NOTURN, 0, "SonicDashPuff", 100, 0, 0, "ArmorBonus", "sweapons/finalhit");
			}
			else if (CountInv("PowerHyper"))
			{
				A_CustomPunch(320, CPF_PULLIN | CPF_NOTURN, 0, "SonicDashPuff", 100, 0, 0, "ArmorBonus", "sweapons/finalhit");
			}
		}
		TNT1 A 1 A_SetInventory("KickCooldown", 10);
		Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 4;
		}
	}
}

Class SonicKickPuff : BulletPuff
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		RenderStyle "None";
		Alpha 0;
		VSpeed 1;
		Mass 5;
		DamageType "SonicKick";
		ProjectileKickBack 1000;
	}
	States
	{
		Spawn:
			TNT1 A 0;
		Melee:
			TNT1 A 0;
			Stop;
	}
}


Class AmyKickPuff : BulletPuff
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		RenderStyle "None";
		Alpha 0;
		VSpeed 1;
		Mass 5;
		DamageType "SonicKick";
		ProjectileKickBack 9999999;
	}
	States
	{
		Spawn:
			TNT1 A 0;
		Melee:
			TNT1 A 0;
			Stop;
	}
}

Class SonicDashPuff : BulletPuff
{
	Default
	{
		+NOBLOCKMAP;
		+NOGRAVITY;
		RenderStyle "None";
		Alpha 0;
		VSpeed 1;
		Mass 5;
		DamageType "SonicKick";
		ProjectileKickBack 1000000;
	}
	States
	{
		Spawn:
			TNT1 A 0;
		Melee:
			TNT1 A 0;
			Stop;
	}
}

Class KickCooldown : Inventory
{
	Default
	{
		Inventory.MaxAmount 25;
	}
}