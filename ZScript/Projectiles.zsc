class ChaosRocket : Actor
{
	bool wasKicked;
	Default
	{
		Radius 10;
		Height 10;
		Speed 20;
		Damage 25;
		ExplosionDamage 100;
		ExplosionRadius 200;
		Projectile;
		ProjectileKickBack 0;
		+RANDOMIZE
		+ROCKETTRAIL
		+SHOOTABLE
		+NOTARGETSWITCH
		+DONTTHRUST
		PainChance "SonicKick", 255;
		SeeSound "sweapons/missilefire";
		DeathSound "weapons/rocklx";
	}
	States
	{
		Spawn:
			MISL A 1 Bright {
				int buttons = target.GetPlayerInput(MODINPUT_BUTTONS);
				if (target.player.ReadyWeapon.GetClassName() == "AirStrikeLauncher" && buttons & BT_ALTATTACK) {
					A_StartSound("weapons/rocklx");
					return ResolveState("Death");
				}
				if (!target.CountInv("PowerHyper")) {
					A_ChangeLinkFlags(false);
					// if not in Hyper form, set NOBLOCKMAP to false so that parrying works
					// Hyper form's multiple rockets breaks the thing when NOBLOCKMAP is false,
					// so it'll be left on in Hyper form
				}
				if (invoker.wasKicked) {
					A_SeekerMissile(30, 45, SMF_LOOK, 255);
					A_ScaleVelocity(1.15);
				}
				return ResolveState(null);
			}
			Loop;
		Pain:
			TNT1 A 0 {
				ACS_NamedExecute("ParryEffect");
				invoker.bShootable = false;
				invoker.bSeekerMissile = true;
				invoker.wasKicked = true;
			}
			Goto Spawn;
		Death:
			MISL B 4 Bright A_Explode();
			MISL C 3 Bright;
			MISL D 2 Bright;
			Stop;
	}
}

class AirstrikeRocket : ChaosRocket
{
	Default
	{
		Damage 100;
	}
}

class RapidGrenade : ChaosRocket
{
	Default
	{
		-NOGRAVITY;
		Speed 40;
		Damage 145;
		ExplosionDamage 75;
	}
}

class DuellingRocket : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 30;
		Damage 400;
        Scale 1.5;
		Projectile;
		ProjectileKickBack 0;
		+RANDOMIZE
		+ROCKETTRAIL
		SeeSound "sweapons/missilefire";
		DeathSound "weapons/rocklx";
	}
	States
	{
		Spawn:
			MISL A 1 Bright A_SeekerMissile(30, 45, SMF_LOOK, 100);
			Loop;
		Detonate:
			TNT1 A 0 A_PlaySound("weapons/rocklx", 1024);
		Death:
			MISL B 4 Bright A_Explode(200, 300);
			MISL C 3 Bright;
			MISL D 2 Bright;
			Stop;
	}
}

class DuellingRocketUnguided : DuellingRocket
{
	States
	{
		Spawn:
			MISL A 1 Bright;
			Loop;
	}
}

class ChaosMissile : FastProjectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 70;
		Damage 10;
		Scale 0.7;
		Projectile;
		+RANDOMIZE
		+DEHEXPLOSION
		+ROCKETTRAIL
		ProjectileKickBack 999999999;
		SeeSound "sweapons/missilefire";
		DeathSound "weapons/rocklx";
	}
	States
	{
		Spawn:
			MISL A 1 Bright;
			Loop;
		Death:
			MISL B 4 Bright A_Explode;
			MISL C 3 Bright;
			MISL D 2 Bright;
			Stop;
	}
}

class StreamRocket : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 40;
		Damage 20;
		DamageType "RocketStream";
		Projectile;
		+RANDOMIZE
		+DEHEXPLOSION
		+ROCKETTRAIL
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		Obituary "$OB_MPROCKET"; // "%o rode %k's rocket."
	}
	States
	{
		Spawn:
			MISL A 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_RadiusThrust(800);
			MISL B 4 Bright A_Explode;
			MISL C 3 Bright;
			MISL D 2 Bright;
			Stop;
	}
}

class ChaosEnergyBall : FastProjectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 100;
		Damage 15;
		DamageType "Eclipse";
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
	}
	States
	{
		Spawn:
			PLSS AB 6 Bright;
			Loop;
		Death:
			PLSE ABCDE 4 Bright;
			Stop;
	}
}

class ChaosEnergyBallEX : FastProjectile
{
	Default
	{
		Radius 13;
		Height 8;
		Scale 2;
		Speed 100;
		Damage 32;
		DamageType "Eclipse";
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
	}
	States
	{
		Spawn:
			PLSX AB 6 Bright;
			Loop;
		Death:
			PLXE ABCDE 4 Bright;
			Stop;
	}
}

class MinorChaosEnergyBall : FastProjectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 100;
		Damage 5;
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		DamageType "Eclipse";
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
	}
	States
	{
		Spawn:
			PLSS AB 6 Bright;
			Loop;
		Death:
			PLSE ABCDE 4 Bright;
			Stop;
	}
}

class NerfedChaosEnergyBall : MinorChaosEnergyBall
{
	Default
	{
		Damage 2;
		Speed 50;
	}
}

class SuperChaosEnergyBall : FastProjectile
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 80;
		Damage 70;
		DamageType "Eclipse";
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "sweapons/chaosspear";
		DeathSound "weapons/bfgx";
	}
	States
	{
		Spawn:
			BFS1 AB 6 Bright;
			Loop;
		Death:
			BFE1 AB 4 Bright;
			BFE1 C 4 Bright A_BFGSpray();
			BFE1 DEF 4 Bright;
			Stop;
	}
}

class ChaosEnergyBallAmy : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		Damage 40;
		DamageType "Eclipse";
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
	}
	States
	{
		Spawn:
			PRSS AB 6 Bright
			{
				if (CountInv("LegOut", AAPTR_TARGET) > 0 || CountInv("PowerSuper", AAPTR_TARGET) > 0)
				{
					A_ScaleVelocity(2);
					A_SetDamageType("EclipseEX");
				}
			}
			Loop;
		Death:
			PRSE ABCDE 4 Bright;
			Stop;
	}
}

class ParryProjectile : FastProjectile
{
	Default
	{
		Scale 1.5;
		Radius 13;
		Height 8;
		Speed 1;
		Projectile;
		+RANDOMIZE
		+EXTREMEDEATH
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		DeathSound "weapons/rocklx";
	}
	States
	{
		Spawn:
			PRSS AB 6 Bright;
			Loop;
		Death:
			TNT1 A 0 A_Explode(128, 256, 0);
			MISL B 7 Bright;
			MISL C 5 Bright;
			MISL D 3 Bright;
			Stop;
	}
}

class Shuriken : FastProjectile
{
	Default
	{
		Radius 8;
		Height 2;
		Speed 100;
		Damage 20;
		Projectile;
		+RIPPER
		+NOEXTREMEDEATH
		RenderStyle "Normal";
		Alpha 1;
		SeeSound "sweapons/shurikenfire";
		RipSound "sweapons/slice";
		DeathSound "sweapons/shurikenhit";
	}
	States
	{
		Spawn:
			SHUR AB 2 { if (CountInv("PowerHyper", AAPTR_Player1)) { A_SetSpeed(300); } }
			Loop;
		Death:
			SHUR A 30;
			Goto Fade;
		Fade:
			SHUR A 1 A_FadeOut(0.1);
			Loop;
	}
}

class ChaosSpear : FastProjectile
{
	Default
	{
		Radius 5;
		Height 4;
		Speed 120;
		Damage 40;
		Projectile;
		+SEEKERMISSILE
		RenderStyle "Add";
		DamageType "Spear";
		Alpha 0.75;
		ProjectileKickBack 1;
	}
	States
	{
		Spawn:
			CSPE A 1 Bright A_SpawnItem("ChaosSpearTrail");
			Loop;
		Death:
			TNT1 A 0 A_PlaySound("sweapons/spearimpact");
		Ending:
			CSPE A 1 Bright A_FadeOut(0.1, 0);
			Loop;
	}
}

class ChaosSpearEX : ChaosSpear { Default { Damage 80; } }

class ChaosSpearTrail : Actor
{
	Default
	{
		Radius 5;
		Height 4;
		+NOGRAVITY;
	}
	States
	{
		Spawn:
			CSPE A 1 Bright A_FadeOut(0.1, 0);
			Loop;
	}
}

class EnergyBladeLeft : FastProjectile
{
	Default
	{
		Radius 8;
		Scale 0.5;
		Height 2;
		Speed 80;
		Damage 8;
		Projectile;
		+RIPPER
		+EXTREMEDEATH
		RenderStyle "Normal";
		Alpha 1;
	}
	States {
		Spawn:
			ENSL A 12;
			TNT1 A 0 A_Die();
			Stop;
		Death:
			Stop;
	}
}

class EnergyBladeRight : EnergyBladeLeft
{
	States {
		Spawn:
			ENSR A -1;
			Loop;
	}
}

class HyperEnergyBladeLeft : EnergyBladeLeft
{
	Default
	{
		Scale 0.6;
		Damage 16;
	}
	States {
		Spawn:
			ENSL B -1;
			Loop;
		Death:
			Stop;
	}
}

class HyperEnergyBladeRight : HyperEnergyBladeLeft
{
	States {
		Spawn:
			ENSR B -1;
			Loop;
	}
}

class ProjectileBullet : FastProjectile
{
    Default
    {
        Radius 4;
        Height 4;
        Speed 50;
        Damage 5;
        Projectile;
        SeeSound "grunt/attack";
    }
    States
    {
        Spawn:
            PUFF A 1 Bright;
            Loop;
        Death:
            TNT1 A 1;
            Stop;
    }
}

class ProjectilePellet : ProjectileBullet
{
    Default
    {
        Radius 4;
        Height 4;
        Speed 40;
        Damage 1;
        Projectile;
        SeeSound "shotguy/attack";
    }
    States
    {
        Spawn:
            PUFF A 1 Bright;
            Loop;
        Death:
            TNT1 A 1;
            Stop;
    }
}

class WeakProjectileBullet : ProjectileBullet
{
    Default
    {
        Damage 1;
    }
}
