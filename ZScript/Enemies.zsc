class ActualLostSoul : LostSoul replaces LostSoul 
{
	Default
	{
		DropItem "SoulRing";
	}
}

class FastSpiderdemon : SpiderMastermind // 19924
{
	Default
	{
		Tag "E-SP567";
		Speed 200;
		Radius 60;
		Height 100;
		Mass 100000000;
		Obituary "%o isn't very good at secret bosses.";
		PainChance 0;
		PainChance "Eclipse", 256;
		DamageFactor "Eclipse", 0.01;
		DamageFactor "HomingAttack", 0;
		DamageFactor "SonicKick", 2;
	}
	States
	{
		Spawn:
			SPID AB 10 A_LookEx(LOF_FULLVOLSEESOUND, 50, 0, 0, 30, "See");
			Loop;
		See:
			SPID A 1 A_Metal;
			SPID ABB 1 A_Chase;
			SPID C 1 A_Metal;
			SPID CDD 1 A_Chase;
			SPID E 1 A_Metal;
			SPID EFF 1 A_Chase;
			Loop;
		Missile:
			SPID A 10 Bright A_FaceTarget;
			TNT1 A 0 A_JumpIf(health < 1500, "SecPhasStart");
			SPID GH 1 Bright A_SpawnProjectile("PlasmaBall");
			SPID H 1 Bright A_SpidReFire();
			Goto Missile+1;
		SecPhasStart:
			SPID A 5 Bright A_PlaySound("spider/sight"); 
		PMissile:
			TNT1 A 0 ACS_NamedExecute("BossMusic2");
			SPID A 10 Bright A_FaceTarget;
			SPID G 1 Bright A_SpawnProjectile("ChaosEnergyBall");
			SPID H 1 Bright A_SpawnProjectile("ChaosEnergyBallAmy");
			SPID H 1 Bright A_SpidReFire();
			Goto PMissile+2;
		Pain:
			TNT1 A 0 A_JumpIf(health < 1500, "Pain2");
			SPID I 12 A_Pain;
			Goto See;
		Pain2:
			SPID I 8 A_Pain;
			Goto See;
		Death:
			TNT1 A 0 A_StartSound("sonic/bossvictory", 8, 0, 1.0, ATTN_NONE);
			SPID J 20 A_Scream;
			SPID K 10 
			{
				A_NoBlocking();
				A_SpawnItem("GoalRing");
			}
			SPID LMNOPQR 10;
			SPID S 30;
			SPID S -1;
			Stop;
	}
}

class MechZombie : Actor
{
	Default
	{
		Health 80;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 150;
		PainChance "Spear", 256;
		DamageFactor "Spear", 0.1;
		Monster;
		+FLOORCLIP;
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		Obituary "%o was destroyed by a mechanised zombie.";
		Tag "Mechanised Zombie";
		DropItem "SoulRing";
		//$Category Lock & Load Enemies
	}
 	States
	{
	Spawn:
		POSS AB 10 A_Look;
		Loop;
	See:
		POSS ABCD 2 A_Chase;
		Loop;
	Melee:
		POSS F 2 A_M_Saw();
		POSS F 0 A_JumpIfCloser(40, "Melee");
		Goto See;
	Missile:
		POSS A 0 A_FaceTarget;
		POSS FFF 1 A_SpawnProjectile("ChaosEnergyBall");
		POSS E 24;
		Goto See;
	Pain.Spear:
		TNT1 A 0 ACS_NamedExecute("ReactionTime");
		POSS GGGGGGGGGGG 6 A_Pain;
	Pain:
		POSS G 6 A_Pain;
		Goto See;
	Death:
		POSS H 5;
		POSS I 5 A_Scream;
		POSS J 5 A_NoBlocking;
		POSS K 5;
		POSS L -1;
		Stop;
	XDeath:
		POSS M 5;
		POSS N 5 A_XScream;
		POSS O 5 A_NoBlocking;
		POSS PQRST 5;
		POSS U -1;
		Stop;
	Raise:
		POSS K 5;
		POSS JIH 5;
		Goto See;
	}
}

class WalkingCapsule : Demon
{
	Default
	{
		DropItem "BlueChaosCapsule";
		//$Category Lock & Load Enemies
	}
	States
	{
		Spawn:
			SARG AB 10 A_Look;
			Loop;
	}
}

class FasterSpiderdemon : SpiderMastermind // 19925
{
	Default
	{
		Tag "E-SP666";
		Speed 250;
		Radius 60;
		Height 100;
		Mass 100000000;
		Health 5865044;
		Obituary "0x5865044 FATAL: Traceback: player %o experienced gateway failure";
		DamageFactor "Normal", 0;
	}
	States
	{
		Spawn:
			SPID AB 2 A_LookEx(LOF_FULLVOLSEESOUND, 50, 0, 0, 30, "See");
			Loop;
		See:
			SPID A 1 A_Metal;
			SPID ABB 1 A_Chase;
			SPID C 1 A_Metal;
			SPID CDD 1 A_Chase;
			SPID E 1 A_Metal;
			SPID EFF 1 A_Chase;
			Loop;
		Missile:
			SPID A 2 Bright A_FaceTarget;
			SPID G 1 Bright A_SpawnProjectile("PlasmaBall");
			SPID H 1 Bright A_SpawnProjectile("PlasmaBall");
			SPID H 1 Bright A_SpidReFire();
			Goto Missile+1;
		Pain:
			SPID I 2;
			SPID I 2 A_Pain;
			Goto See;
		Death:
			SPID J 20 A_Scream;
			SPID K 10 A_NoBlocking;
			SPID LMNOPQR 10;
			SPID S 30;
			SPID S -1 A_BossDeath;
			TNT1 A 0 Exit_Normal(0);
			Stop;
	}
}

class FrickingCyberdemon : Actor
{
	Default
	{
		Health 16000;
		Radius 50;
		Height 160;
		Mass 4000;
		Speed 25;
		Scale 1.5;
		PainChance 20;
		PainChance "Eclipse", 255;
		PainChance "Spear", 255;
		DamageFactor "SonicKick", 4;
		DamageFactor "Piko", 4;
		DamageFactor "ShadowMelee", 2;
		Monster;
		MinMissileChance 160;
		+BOSS 
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		SeeSound "cyber/sight";
		PainSound "cyber/pain";
		DeathSound "cyber/death";
		ActiveSound "cyber/active";
		Obituary "%o got obliterated by the fricking Cyberdemon.";
		Tag "Fricking Cyberdemon";
	}
	States
	{
		Spawn:
			CYBR AB 10 A_Look;
			Loop;
		See:
			CYBR A 2 A_Hoof;
			CYBR ABBCC 2 A_Chase;
			CYBR D 2 A_Metal;
			CYBR D 2 A_Chase;
			Loop;
		Missile:
			CYBR A 0 A_JumpIfCloser(400, "RocketAttack");
		Boost:
			CYBR E 6 A_FaceTarget;
			CYBR FFFFFFFFFFFFFF 1 {
				A_StartSound("sweapons/boost", 8);
				A_Recoil(-15);
			}
			Goto See;
		RocketAttack:
			CYBR E 6 A_FaceTarget;
			CYBR F 3 A_CyberAttack;
			CYBR F 3 A_CyberAttack;
			CYBR F 3 A_CyberAttack;
			CYBR F 3 A_CyberAttack;
			CYBR F 18 A_CyberAttack;
			Goto See;
		Pain.Spear:
			TNT1 A 0 ACS_NamedExecute("ReactionTime");
			CYBR GGGGGGGG 10 A_Pain;
			Goto See;
		Pain:
			CYBR G 30 A_Pain;
			Goto See;
		Death:
			CYBR H 10;
			CYBR I 10 A_Scream;
			CYBR JKL 10;
			CYBR M 10 A_NoBlocking;
			CYBR NO 10;
			CYBR P 30;
			CYBR P -1 A_BossDeath;
			Stop;
	}
}