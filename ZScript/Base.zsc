class LockLoadPlayer : PlayerPawn 
{
	int dJumpCounter;
	double oldz;
	bool reticleActive;
	Default
	{
		Speed 4;
		Player.ForwardMove 1, 0.5;
		Player.SideMove 1, 0.5;
		Health 125;
		Radius 15;
		Height 42;
		Player.ViewHeight 42.0;
		Player.AttackZOffset 16.5;
		PainChance 250;
		Mass 100;
		Player.MaxHealth 999;
		Player.JumpZ 15.0;
	}
	override int TakeSpecialDamage(Actor inflictor, Actor source, int damage, Name damagetype)
	{
		Super.TakeSpecialDamage(inflictor, source, damage, damagetype);
		if (damage > 0 && damage < 999 && !(player.cheats & CF_GODMODE) && !CountInv("PowerInvulnerable")) {
			if (health >= 20 && damage > health) {
				damage = health - 1;
			}
            A_StartSound("sglobal/loserings", 6);
			int ringcount = damage;
			int maxrange  = damage / 4;
			if (maxrange < 5) maxrange = 5;
			if (maxrange > 15) maxrange = 15;
			if (ringcount > 150) ringcount = 150;
			for (int x = 1; x <= ringcount; x++) {
				int x = random(maxrange * -1, maxrange);
				int y = random(maxrange * -1, maxrange);
				A_SpawnItemEx("DroppedRingPlayer", 0, 0, 1, x, y, 15);
			}
		}
		return damage;
	}
	override void FallAndSink(double grav, double oldfloorz)
	{
		if (waterlevel >= 2 && pos.z > oldfloorz && Vel.Z <= 5) Vel.Z -= Gravity;
		Super.FallAndSink(grav, oldfloorz);
	}
	States
	{
		Spawn:
			PLAY A -1;
			Loop;
		See:
			PLAY ABCD 4;
			Loop;
		Missile:
			PLAY E 12;
			Goto Spawn;
		Melee:
			PLAY F 6 BRIGHT;
			Goto Missile;
		Pain:
			PLAY G 4;
			PLAY G 0; 
			PLAY G 4;
			Goto Spawn;
		Death.Drownd:
			TNT1 A 0 A_PlaySound("sglobal/drown");
			Goto Death+2;
		Death:
			TNT1 A 0;
			TNT1 A 0 ACS_NamedExecute("DeathMusic", 0, 0);
			PLAY H 10;
			PLAY I 10;
			PLAY J 10 A_NoBlocking;
			PLAY KLM 10;
			PLAY N -1;
			Stop;
		XDeath:
			TNT1 A 0 ACS_NamedExecute("DeathMusic", 0, 0);
			TNT1 A 0;
			TNT1 A 0;
			PLAY O 5;
			PLAY P 5;
			PLAY Q 5 A_NoBlocking;
			PLAY RSTUV 5;
			PLAY W -1;
			Stop;
	}
	override void Tick()
	{
		bool WJumpPrerequisite = false;
		switch (GetCvar("WallJumpPrerequisite")) {
			case 0: // Wall jump at any time
				WJumpPrerequisite = true;
				break;
			case 1: // Wall jump only if holding forward
				if (GetPlayerInput(MODINPUT_FORWARDMOVE) >= 6400)
					WJumpPrerequisite = true;
				break;
			case 2: // Wall jump only if not holding forward
				if (GetPlayerInput(MODINPUT_FORWARDMOVE) < 6400)
					WJumpPrerequisite = true;
				break;
		}
		cvar.FindCvar("CanWallJump").SetBool(CheckLOF(CLOFF_JUMP_ON_MISS|CLOFF_SKIPENEMY|CLOFF_SKIPFRIEND|CLOFF_SKIPOBJECT|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL|CLOFF_NOAIM_VERT, 32) && WJumpPrerequisite);
		cvar.FindCvar("CloseToEnemy").SetBool(CheckLOF(CLOFF_JUMPENEMY|CLOFF_SKIPOBJECT|CLOFF_MUSTBESHOOTABLE|CLOFF_JUMPNONHOSTILE, 128));
		oldz = Pos.Z;
		FTranslatedLineTarget ltinfo;
		if (GetCvar("HomingSnapReticle") && CountInv("SonicCheck") || (CountInv("ShadowCheck") && !player.onground)) {
			bool ltattack = LineAttack(angle, 896, pitch, 0, 'None', "InvisiblePuff", LAF_NORANDOMPUFFZ, ltinfo, 0);
			let ltarget = ltinfo.linetarget;
			if (ltarget && Distance3D(ltarget) > 128 && health > 0) {
				if (CountInv("SonicCheck") || (CountInv("ShadowCheck") && ltarget.bIsMonster)) {
					if (!reticleActive) {
						A_StartSound("sweapons/reticle", 11);
						reticleActive = true;
					}
					ACS_NamedExecute("HomingReticle");
				}
			}
			else {
				reticleActive = false;
			}
			cvar.FindCvar("CanHoming").SetBool(reticleActive);
		}
		Super.Tick();
	}
	override void CheckJump()
	{
		let player = self.player;
		double wJumpHeight;
		double wJumpHeightOffset = 50;
		double wJumpDistance = 25;
		bool isTouchingWall = GetCvar("CanWallJump");
		int buttons = GetPlayerInput(MODINPUT_BUTTONS);
		int dJumpCount = 1; // You get an extra jump
		if (CountInv("ClassicCheck") && !CountInv("PowerHyper")) {
			dJumpCount = 0; // ... except if you're Classic Sonic
		}
		if (CountInv("SonicCheck") && CountInv("PowerHyper")) {
			dJumpCount = 2; // Two extra jumps if you're Hyper Sonic
		}
		if (player.onground) {
			dJumpCounter = 0; // Reset the double jump counter once you touch the ground
			cvar.FindCvar("DidJump").SetBool(false); // Reset the DidJump CVAR as well
		}
		bool IsBoostingMidAir = (CountInv("SonicCheck") && player.ReadyWeapon.GetClassName() == "Caliburn" && (CountInv("PowerSuper") || CountInv("PowerHyper")) && (buttons & BT_ALTATTACK) && !player.onground);
		cvar.FindCvar("CanJump").SetBool(dJumpCounter < dJumpCount);
		if (GetPlayerInput(MODINPUT_BUTTONS) & BT_JUMP && !(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_JUMP) && !IsBoostingMidAir) {
			int buttons = GetPlayerInput(MODINPUT_BUTTONS);
			bool IsHovering = buttons & BT_ALTATTACK && (CountInv("PowerSuper")||CountInv("PowerHyper")) && !player.onground;
			if (bNoGravity) {
				Vel.Z = 3.;
			}
			else if (level.IsJumpingAllowed() && player.onground) {
				// Usual jump stuff
				cvar.FindCvar("DidJump").SetBool(true);
				double jumpvelz = JumpZ * 35 / TICRATE;
				double jumpfac = 0;

				if (jumpfac > 0) {
					jumpvelz *= jumpfac;
				}
				
				if (player.crouchoffset != 0) {
					player.crouching = 1;
				}

				double speed = sqrt((Vel.X ** 2) + (Vel.Y ** 2));
				double slopebonus = (Pos.Z - oldz) * (speed / 57);
				if (slopebonus < 0)  slopebonus = 0;
				if (slopebonus > 15) slopebonus = 15;
				Vel.Z = jumpvelz + slopebonus;
				bOnMobj = false;
				player.jumpTics = 0;
				if (!(player.cheats & CF_PREDICTING)) {
					A_StartSound("*jump", CHAN_BODY);
				}
			}
			else if (CountInv("SonicCheck") && isTouchingWall && Pos.Z - GetZAt() > 60) {
				dJumpCounter = 0; // You get a free double jump after a wall jump
				if (Vel.Z >= 1) {
					wJumpHeight = Vel.Z + wJumpHeightOffset;
				}
				else {
					wJumpHeight = wJumpHeightOffset;
				}
				ACS_NamedExecute("WallJump", 0, wJumpHeight, wJumpDistance);
			}
			else if (level.IsJumpingAllowed() && !IsHovering) {
				// Double jump stuff
				if (GetCvar("Sliding")) {
					A_ChangeVelocity(Vel.X, Vel.Y, JumpZ - 2, CVF_Replace);
					A_StartSound("*jump", CHAN_BODY);
				}
				else {
					if (dJumpCounter < dJumpCount || waterlevel > 1 && CountInv("SonicCheck")) {
						dJumpCounter++;
						A_ChangeVelocity(Vel.X, Vel.Y, JumpZ - 2, CVF_Replace);
						A_StartSound("*doublejump", CHAN_BODY);
					}
				}
			}
		}
	}
	override bool CanCrouch()
	{
		player = self.player;
		if (!player.onground) {
			return false;
		}
		Super.CanCrouch();
		return true;
	}
}

class LLSwitch : Actor
{
	Default
	{
		+SHOOTABLE
		+NOBLOOD
		+DONTFALL
		+NOGRAVITY
		+NOCLIP
		Health 1;
		Mass 100000;
		YScale 0.9;
		Height 30;
		DamageFactor "Normal", 0;
		DeathSound "switches/normbutn";
	}
	States
	{
		Death:
			ACTS A -1 A_Scream();
			Stop;
	}
}

class LockLoadWeapon : Weapon
{
	override bool DepleteAmmo(bool altFire, bool checkEnough, int ammouse)
	{
		if ((CountInv("PowerSuper", AAPTR_Player1) || CountInv("PowerHyper", AAPTR_Player1))) {
			if (!altfire) {
				A_GiveInventory("ChaosEnergy", AmmoUse1, AAPTR_Player1);
			}
			else {
				A_GiveInventory("ChaosEnergy", AmmoUse2, AAPTR_Player1);
			}
		}
		super.DepleteAmmo(altFire, checkEnough, ammouse);
		return true;
	}
}

class SlamAtkObject : CustomInventory
{
	int inuse;
	int midfall;
	int zdiff;
	action void SlamAttack()
	{
		A_PlaySound("*slam", 1);
		A_Explode(120, 300, XF_NOTMISSILE);
		bool ok;
		Actor below;
		[ok, below] = TestMobjZ(true);
		if (below) {
			below.A_DamageSelf(random(1, 16) * 256);
		}
	}
	action void BoundAttack()
	{
		A_PlaySound("*slam", 2);
		int bounce = invoker.zdiff / 12 + 8;
		if (bounce > 40) bounce = 40;
		if (bounce < 15) bounce = 15;
		A_ChangeVelocity(0, 0, bounce);
		cvar.FindCvar("DidJump").SetBool(true);
		bool ok;
		Actor below;
		[ok, below] = TestMobjZ(true);
		if (below) {
			below.A_DamageSelf(random(1, 16) * 256);
		}
	}
	override void Tick()
	{
		Super.Tick();
		if (owner.player.onground) 
			midfall = false;
	}
	States
	{
		Use:
			TNT1 A 0 A_Overlay(-4, "SlamCheck");
			TNT1 A 0 A_OverlayOffset(-2, 0, 32);
			Fail;
		SlamCheck:
			TNT1 A 0 {
				if (!(GetPlayerInput(MODINPUT_BUTTONS) & BT_ALTATTACK) && !invoker.midfall) {
					invoker.midfall = true;
					invoker.zdiff = Pos.Z - GetZAt();
					return ResolveState("SlamStart");
				}
				return ResolveState(null);
			}
			Stop;
		SlamStart:
			TNT1 A 0 A_PlaySound("*fall", 1);
			/* passthrough */
		SlamAttack:
			TNT1 A 0 A_ChangeVelocity(0, 0, -500, CVF_Replace);
			TNT1 A 1
			{
				if (CountInv("PowerSuper")||CountInv("PowerHyper")) {
					A_CustomPunch(70, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
				else {
					A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
				if (player.onground) return ResolveState("SlamDone");
				A_ChangeVelocity(0, 0, -50, CVF_Relative|CVF_Replace);
				return ResolveState(null);
			}
			Goto SlamAttack+1;
		SlamDone:
			TNT1 A 0 {
				invoker.midfall = false;
				if (CountInv("ClassicCheck")) {
					return ResolveState("BoundDone");
				}
				A_SetSpeed(0);
				SlamAttack();
				return ResolveState(null);
			}
			TNT1 A 10;
			TNT1 A 0 A_SetSpeed(2);
			Stop;
		BoundDone:
			TNT1 A 1 BoundAttack();
			Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0) {
			inuse--;
		}
		int CrouchPressed = owner.GetPlayerInput(INPUT_BUTTONS) & BT_CROUCH && !(owner.GetPlayerInput(INPUT_OLDBUTTONS) & BT_CROUCH);
		if (owner is "PlayerPawn" && !owner.player.onground && CrouchPressed && inuse == 0) {
			owner.UseInventory(self);
			if (owner.CountInv("ClassicCheck")) inuse = 5;
			else inuse = 14;
		}
	}
}
