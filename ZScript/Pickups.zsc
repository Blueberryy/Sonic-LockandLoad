class Ring : CustomInventory replaces HealthBonus // 2014
{
	Default
	{
		//$Category Lock & Load Pickups
		Radius 10;
		Height 10;
		Inventory.PickupMessage "Picked up a ring.";
		Inventory.PickupSound "spickup/ring";
		+COUNTITEM
		+NOGRAVITY
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
		Spawn:
			RIN1 ABCDEFGHIJKLMNOP 1 Bright A_SetScale(1, 0.9);
			Loop;
		Pickup:
			TNT1 A 0
			{
				if (CountInv("AmyCheck") > 0) {
					if (CountInv("PowerSuper") > 0) { A_GiveInventory("ChaosEnergy", 1); }
					else { A_GiveInventory("Health", 1); }
				}
				else {
					A_GiveInventory("Health", 1);
				}
			}
			Stop;
	}
}

class DroppedRing : Ring { Default { -NOGRAVITY; +CANPASS; } }

class RingBox : Inventory replaces Stimpack // 2011
{
	Default
	{
		//$Category Lock & Load Pickups
		Health 1;
		Radius 10;
		Height 20;
		Inventory.PickupMessage "Broke open a box of rings.";
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		+SHOOTABLE
		+NOBLOOD
	}
	override bool TryPickup(in out Actor toucher)
	{
		A_Die();
		return true;
	}
	override int DamageMobj(Actor inflictor, Actor source, int damage, name mod, int flags, double angle)
	{
		Super.DamageMobj(inflictor, source, damage, mod, flags, angle);
		A_StartSound("spickup/boxdestroy", 2);
		int ringcount = 10;
		int maxrange  = damage / 4;
		if (maxrange < 5) {
			maxrange = 5;
		}
		for (int x = 1; x <= ringcount; x++) {
			int x = random(maxrange * -1, maxrange);
			int y = random(maxrange * -1, maxrange);
			A_SpawnItemEx("DroppedRing", 0, 0, 1, x, y, 10, 0, SXF_NOCHECKPOSITION);
		}
		return damage;
	}
	States
	{
		Spawn:
			RBOX A -1 Bright;
			Stop;
		Death:
			TNT1 A 0;
			Stop;
	}
}

class SpecialRing : Ring replaces Medikit // 2012
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a Special ring.";
		Inventory.PickupSound "spickup/specialring";
		+COUNTITEM
		+NOGRAVITY
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
		Spawn:
			RIN2 ABCDEFGHIJKLMNOP 2 NoDelay Bright A_SetScale(2, 1.7);
			Loop;
		Pickup:
			TNT1 A 0
			{
				if (CountInv("AmyCheck") > 0)
				{
					if (CountInv("PowerSuper") > 0)
					{
						A_GiveInventory("ChaosEnergy", 50);
					}
					else
					{
						A_GiveInventory("ChaosEnergy", 50);
						if (Health < 300)
						{
							A_GiveInventory("Health", 50);
							if (Health > 300)
							{
								A_SetHealth(300);
							}
						}
					}
				}
				else
				{
					A_GiveInventory("ChaosEnergy", 50);
					if (Health < 300)
					{
						A_GiveInventory("Health", 50);
						if (Health > 300)
						{
							A_SetHealth(300);
						}
					}
				}
			}
			Stop;
  }
}

class ChaosRing : Ring replaces ArmorBonus // 2015
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a Chaos ring.";
		Inventory.PickupSound "spickup/ring";
		+COUNTITEM
		+NOGRAVITY
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
		Spawn:
			RIN3 ABCDEFGHIJKLMNOP 1 Bright A_SetScale(1, 0.9);
			Loop;
		Pickup:
			TNT1 A 5
			{
				if (CountInv("AmyCheck") > 0)
				{
					if (CountInv("PowerSuper") > 0)
					{
						A_GiveInventory("ChaosEnergy", 6);
					}
					else
					{
						A_GiveInventory("Health", 5);
						A_GiveInventory("ChaosEnergy", 1);
					}
				}
				else
				{
					A_GiveInventory("Health", 5);
					A_GiveInventory("ChaosEnergy", 1);
				}
			}
			Stop;
		} 
}

class ChaosRingGravity : ChaosRing { Default { -NOGRAVITY } }

class SoulRing : Ring
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a Soul ring.";
		Inventory.PickupSound "spickup/ring";
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
	}
	States
	{
		Spawn:
			RIN4 ABCDEFGHIJKLMNOP 1 Bright A_SetScale(1, 0.9);
			Loop;
		Pickup:
			TNT1 A 5 A_GiveInventory("ChaosEnergy", 50);
			Stop;
	}
}

class SoulRecharge : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Picked up a box of Soul rings!";
		Inventory.PickupSound "spickup/specialring";
	}
	States
	{
		Spawn:
			PSTR A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 A_GiveInventory("ChaosEnergy", 200);
			Stop;
	}
}
class EmeraldRecharge : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Picked up an Emerald recharge!";
		Inventory.PickupSound "spickup/specialring";
	}
	States
	{
		Spawn:
			CPAK A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 A_GiveInventory("ChaosEnergy", 500);
			Stop;
	}
}
class InvincibilityBox : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Invincibility!";
		Inventory.PickupSound "spickup/boxdestroy";
		+COUNTITEM
	}
	States
	{
		Spawn:
			INVC A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0
			{
				if (CountInv("ClassicCheck", AAPTR_PLAYER1)) {
					A_GiveInventory("ClassicInvincibility", 1);
				}
				else {
					A_GiveInventory("Invincibility", 1);
				}
			}
			TNT1 A 0 ACS_NamedExecute("Invincibility");
			Stop;
	}
}
class ShadowFever : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Shadow Fever!";
		Inventory.PickupSound "spickup/boxdestroy";
		+COUNTITEM
	}
	States
	{
		Spawn:
			SHAF A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 A_GiveInventory("Invincibility", 1);
			TNT1 A 0 ACS_NamedExecute("Invincibility");
			Stop;
	}
}
class ChaosEmeralds : CustomInventory 
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "";
		Inventory.PickupSound "spickup/chaosemerald";
		+COUNTITEM
		+INVENTORY.INVBAR
	}
	States
	{
		Spawn:
			CHSM A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 ACS_NamedExecute("ChaosEmeraldHandler");
			Stop;
		Use:
			TNT1 A 0 A_TakeInventory("ChaosEmeralds", 1);
			TNT1 A 0
			{
				if (CountInv("AmyCheck") > 0) {
					A_SetHealth(1);
				}
				else {
					A_SetInventory("Health", 999);
				}
				if (CountInv("ChaosEnergy") < 100) {
					A_SetInventory("ChaosEnergy", 100);
				}
			}
		SuperTransform:
			TNT1 A 0 A_JumpIf(CountInv("PowerSuper"), "HyperTransform");
			TNT1 A 0 {
				A_GiveInventory("SuperForm", 1);
				A_GiveInventory("SuperInvincibility", 1);
				A_PlaySound("sglobal/supersonic", 6);
				A_PlaySound("sglobal/supertransform", 4);
				if (CountInv("SonicCheck"))  {
					ACS_NamedExecute("Subtitles", 0, 5);
				}
				else if (CountInv("AmyCheck")) {
					ACS_NamedExecute("Subtitles", 0, 6);
				}
				else if (CountInv("ShadowCheck")) {
					ACS_NamedExecute("Subtitles", 0, 7);
				}
				else if (CountInv("ClassicCheck")) {
					ACS_NamedExecute("Subtitles", 0, 8);
				}
			}
			TNT1 A 0 ACS_NamedExecute("TransformSuper");
			TNT1 A 0 ACS_NamedExecuteAlways("TakeRingsIfSuper");
			Stop;
		HyperTransform:
			TNT1 A 0 {
				A_TakeInventory("PowerSuper", 1);
				A_GiveInventory("HyperForm", 1);
				A_PlaySound("sglobal/supersonic", 6);
				A_PlaySound("sglobal/supertransform", 4);
				if (CountInv("SonicCheck"))  {
					A_StartSound("super/sonich", 6);
					ACS_NamedExecute("Subtitles", 0, 9);
				}
				else if (CountInv("ClassicCheck")) {
					A_StartSound("super/classich", 6);
					ACS_NamedExecute("Subtitles", 0, 10);
				}
			}
			TNT1 A 0 ACS_NamedExecute("TransformHyper", 0);
			TNT1 A 0 ACS_NamedExecute("TakeRingsIfHyper", 0);
			Stop;
		}
}

class ChaosCapsule : ChaosEnergy // 6021
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a Chaos Energy capsule.";
		Inventory.Amount 20;
	}
	States
	{
		Spawn:
			CHSC A -1 Bright;
			Stop;
	}
}
class BlueChaosCapsule : ChaosEnergy // 6022
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a blue Chaos Energy capsule.";
		Inventory.Amount 100;
	}
	States
	{
		Spawn:
			CHSC B -1 Bright;
			Stop;
	}
}
class RedChaosCapsule : ChaosEnergy // 6023
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "Picked up a red Chaos Energy capsule.";
		Inventory.Amount 400;
	}
	States
	{
		Spawn:
			CHSC C -1 Bright;
			Stop;
	}
}

class MacAndCheese : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Picked up some mac and cheese.";
	}
	States
	{
		Spawn:
			FMAC A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 A_Print("\c[ShadowRedBlack]I'm making mac and cheese,\n\c[ShadowRedBlack]and nobody can stop me!");
			TNT1 A 0 A_GiveInventory("Health", 100);
			TNT1 A 0 A_GiveInventory("ChaosEnergy", 100);
			Stop;
	}
}

class GoalRing : CustomInventory // 262
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "";
		Inventory.PickupSound "spickup/goalring";
		Radius 64;
		Height 100;
		+NOGRAVITY
	}
	States
	{
		Spawn:
			GOAL A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 {
				A_TakeInventory("PowerSuper", 1, 0, AAPTR_PLAYER1);
				A_TakeInventory("PowerHyper");
				ACS_NamedExecute("GoalRing");
			}
			Stop;
	}
}

class Nintendo64 : CustomInventory replaces Infrared // 2045
{
	Default
	{
		//$Category Lock & Load Pickups
		Inventory.PickupMessage "You got the Nintendo 64!";
	}
	States
	{
		Spawn:
			NINT A -1 Bright;
		Pickup:
			NINT A 0 A_GiveInventory("Infrared");
			NINT A 0
			{
				if (CountInv("SonicCheck"))
				{
					A_Print("\c[SonicBlue]Hey, guys! It's me, Sonic!\n\c[SonicBlue]On the Nintendo 64!\n\c[SonicBlue]WOOOOOO!");
					ACS_NamedExecute("SonicN64");
				}
			}
	}
}

class FakeBackpack : CustomInventory
{
	Default
	{
		Inventory.PickupMessage "Picked up a backpack with nothing in it.";
	}
	States
	{
		Spawn:
			BPAK A -1 Bright;
			Stop;
		Pickup:
			TNT1 A 0 
			{
				if (CountInv("SonicCheck")) // If playing as Sonic...
				{
					A_Print("\c[SonicBlue]Just an empty backpack."); // print this message to the screen.
				}
				else if (CountInv("AmyCheck")) // If playing as Amy...
				{
					A_Print("\c[AmyPink]What a pretty backpack!"); // print this message to the screen.
				}
				else if (CountInv("ClassicCheck")) // If playing as Classic Sonic...
				{
					A_Print("\c[ClassicBlue]Oh well, at least it looks cool."); // ... You get it by now.
				}
			}
			Stop;
	}
}

class FakeChainsaw : CustomInventory	
{
	Default
	{
		Inventory.PickupMessage "Picked up a useless chainsaw.";
	}
	States
	{
		Spawn:
			CSAW A -1;
			Stop;
		Pickup:
			TNT1 A 0 A_Print("\c[ClassicBlue]Chainsaw, huh? Yeah, I can't use this.");
			Stop;
	}
}

class WtfIsThis : Inventory
{
	Default { Inventory.PickupMessage ""; }
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				if (CountInv("ShadowCheck", AAPTR_PLAYER1)) {
					A_SpawnItem("ShadowKnife");
				} else {
					A_SpawnItem("RedChaosCapsule");
				}
			}
			Stop;
	}
}

class KeyTablet : Inventory
{
	Default 
	{
		Inventory.PickupMessage "Picked up a Key Tablet.";
		Inventory.PickupSound   "spickup/keytablet";
	}
	States
	{
		Spawn:
			KTBL A -1;
			Stop;
	}
}