class SonicPlayer : LockLoadPlayer
{
	Default
	{
		Health 125;
		Player.DisplayName "$sonic";
		Player.Face "SNF";
		Player.SoundClass "sonic";
		Player.StartItem "AcceleratedPistol";
		Player.StartItem "Caliburn";
		Player.StartItem "SonicCheck";
		Player.StartItem "SonicKick";
		Player.StartItem "HomingSnapQuickStep";
		Player.StartItem "SlamAtkObject";
		Player.StartItem "ChaosEnergy", 100;
		Player.WeaponSlot 1, "Caliburn";
		Player.WeaponSlot 2, "AcceleratedPistol";
		Player.WeaponSlot 3, "RapidShotty";
		Player.WeaponSlot 4, "ShurikenCrossbow";
		Player.WeaponSlot 5, "EclipseBlaster";
		Player.WeaponSlot 6, "AirStrikeLauncher";
	}
	override void Tick()
	{
		if (GetCvar("ReservedMode")) {
			A_SetSpeed(2.5);
		}
		else if (CountInv("PowerSuper")||CountInv("PowerHyper")) {
			A_SetSpeed(8);
		}
		else {
			A_SetSpeed(4);
		}
		Super.Tick();
	}
	States
	{
		Pain.Falling:
			TNT1 A 0 {
				switch (random(0, 2)) {
					case 1:
						A_PlaySound("sonic/thanksalotwall", 7);
						ACS_NamedExecute("Subtitles", 0, 3);
						break;
					case 2:
						A_PlaySound("sonic/thiswallsintheway", 7);
						ACS_NamedExecute("Subtitles", 0, 4);
						break;
					default:
						A_PlaySound("sonic/pain", 7);
						break;
				}
				A_PlaySound("sglobal/loserings", 6);
			}
			Goto Spawn;
		Pain:
			PLAY G 4;
			PLAY G 0 A_PlaySound("sglobal/loserings", 6);
			PLAY G 4 A_PlaySound("sonic/pain", 7);
			Goto Spawn;
		Death:
			TNT1 A 0
			{
				if (random(1, 4) == 4) {
					A_PlaySound("sonic/thatwaslame");
					ACS_NamedExecute("Subtitles", 0, 1);
				}
				else {
					A_PlaySound("sonic/scream");
				}
			}
			TNT1 A 0 ACS_NamedExecute("DeathMusic", 0, 0);
			PLAY H 10;
			PLAY I 10;
			PLAY J 10 A_NoBlocking;
			PLAY KLM 10;
			PLAY N -1;
			Stop;
		XDeath:
			TNT1 A 0 ACS_NamedExecute("DeathMusic", 0, 1);
			TNT1 A 0 A_PlaySound("sonic/ohno");
			TNT1 A 0 ACS_NamedExecute("Subtitles", 0, 2);
			PLAY O 5;
			PLAY P 5;
			PLAY Q 5 A_NoBlocking;
			PLAY RSTUV 5;
			PLAY W -1;
			Stop;
		Reticle:
			RETI B 1;
			Stop;
	}
}

enum ECheckSolidFootingFlags
{
    CSF_SOLIDGROUND = 1,
    CSF_SOLIDACTORS = 2,

    CSF_ALL = CSF_SOLIDGROUND|CSF_SOLIDACTORS,
}

class BoostEnabledWeapon : LockLoadWeapon
{
	int BoostSpeed;
	bool BoostCheck;
	action void Boost(int form, int InitialBoostSpeed, int MaxBoostSpeed)
	{
		if (!invoker.BoostCheck) {
			int speed = sqrt((Vel.X ** 2) + (Vel.Y ** 2));
			invoker.BoostCheck = true;
			if (GetPlayerInput(MODINPUT_BUTTONS) & BT_FORWARD && !(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_FORWARD)) {
				invoker.BoostSpeed = MaxBoostSpeed;
			}
			else if (speed > InitialBoostSpeed) {
				invoker.BoostSpeed = speed;
			}
			else {
				invoker.BoostSpeed = InitialBoostSpeed;
			}
			A_StartSound("sonic/boost", 6);
			A_StartSound("sweapons/boost", 7);
			if (!form) A_TakeInventory("ChaosEnergy", 5);
		}
		switch (form) {
			default:
				/* Sonic */
				A_SetMugShotState("Boost");
				A_FireBullets(60, 60, 150, 1, "BoostPuff", FBF_NORANDOM, 125);
				A_TakeInventory("ChaosEnergy", 2);
				break;
			case 1:
				/* Super Sonic */
				A_SetMugShotState("SuperBoost");
				A_FireBullets(120, 120, 150, 999, "BoostPuff", FBF_NORANDOM, 140);
				A_TakeInventory("ChaosEnergy", 1);
				break;
			case 2:
				/* Hyper Sonic */
				A_SetMugShotState("HyperBoost");
				A_FireBullets(120, 120, 150, 999, "BoostPuff", FBF_NORANDOM, 140);
				break;
		}
		A_PlaySound("sweapons/boosttrail", 1, 1.0, true);
		A_ChangeVelocity(Cos(Pitch) * invoker.BoostSpeed, 0, Vel.Z, CVF_Relative|CVF_Replace);
		if ((GetPlayerInput(MODINPUT_BUTTONS) & BT_JUMP) && !(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_JUMP) && A_CheckFloor("Null")) {
			A_ChangeVelocity(0, 0, invoker.BoostSpeed / 10, CVF_Relative);
		}
		if (form == 2 && GetPlayerInput(MODINPUT_BUTTONS) & BT_JUMP)   A_ChangeVelocity(0, 0,  3);
		if (form == 2 && GetPlayerInput(MODINPUT_BUTTONS) & BT_CROUCH) A_ChangeVelocity(0, 0, -3);
		if (invoker.BoostSpeed <= MaxBoostSpeed) invoker.BoostSpeed += 5;
		A_Light(2);
	}
	action void WallCrash()
	{
		A_StopSound(1);
		invoker.BoostCheck = false;
		int CrashSpeed = ((invoker.BoostSpeed/10)+20)*-1;
		int VerticalCrashSpeed = 0;
		if (Vel.Z >= 1) {
			VerticalCrashSpeed = Vel.Z;
		}
		A_ChangeVelocity(CrashSpeed, 0, VerticalCrashSpeed, CVF_Relative|CVF_Replace);
		A_StartSound("sweapons/finalhit", 8);
		DamageThing(invoker.BoostSpeed / 10, 17);
	}
	States
	{
		Ready:
			TNT1 A 1;
			Loop;
		Deselect:
			#### A 1 A_Lower();
			Loop;
		Select:
			#### A 1 A_Raise();
			Loop;
		Fire:
			TNT1 A 0;
			Goto Ready;
		BoostStart:
			TNT1 A 0 A_JumpIf(CountInv("PowerHyper"), "HyperBoost");
			TNT1 A 0 A_JumpIf(CountInv("PowerSuper"), "SuperBoost");
			TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy"), "Boost");
			Goto BoostEnd;
		Boost:
			BOOS A 1 Boost(0, 75, 175);
			BOOS A 0 A_CheckLOF("WallCrash", CLOFF_JUMP_ON_MISS|CLOFF_SKIPENEMY|CLOFF_SKIPFRIEND|CLOFF_SKIPOBJECT|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL|CLOFF_NOAIM_VERT, 32);
			BOOS A 0 {
				if (Pos.Z - GetZAt() < 60) {
					A_ReFire();
				}
			}
			Goto BoostEnd;
		SuperBoost:
			BOOS B 1 Boost(1, 100, 200);
			BOOS B 0 A_CheckLOF("WallCrash", CLOFF_JUMP_ON_MISS|CLOFF_SKIPENEMY|CLOFF_SKIPFRIEND|CLOFF_SKIPOBJECT|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL|CLOFF_NOAIM_VERT, 32);
			BOOS B 0 A_ReFire();
			Goto BoostEnd;
		HyperBoost:
			BOOS C 1 Boost(2, 100, 999);
			BOOS C 0 A_CheckLOF("WallCrash", CLOFF_JUMP_ON_MISS|CLOFF_SKIPENEMY|CLOFF_SKIPFRIEND|CLOFF_SKIPOBJECT|CLOFF_MUSTBESOLID|CLOFF_ALLOWNULL|CLOFF_NOAIM_VERT, 32);
			BOOS C 0 A_ReFire();
			Goto BoostEnd;
		WallCrash:
			TNT1 A 0 A_JumpIf(!GetCvar("WallCrash"), "BoostEnd");
			TNT1 A 0 WallCrash();
		BoostEnd:
			BOOS # 5 {
				invoker.BoostCheck = false;
				A_StopSound(1);
				A_Light(0);
			}
			Goto Ready;
	}
}

class Caliburn : BoostEnabledWeapon
{
	double oldVelZ;
	override void Tick()
	{
		oldVelZ = vel.z;
		super.Tick();
	}
	action state A_CheckSolidFooting(StateLabel label, int flags = CSF_ALL)
	{
		if (invoker.oldVelZ != vel.z) {
			return null;
		}
		if (abs(pos.z - GetZAt()) <= 1) {
			if (flags & CSF_SOLIDGROUND) {
				return ResolveState(label);
			}
		}
		bool ok; Actor below;
		[ok, below] = TestMobjZ(true);
		if (below) {
			if (flags & CSF_SOLIDACTORS) {
				return ResolveState(label);
			}
		}
		if (vel.z == invoker.oldVelZ && (flags & CSF_SOLIDGROUND)) {
			return ResolveState(label);
		}
		return null;
	}
	Default
	{
		Weapon.SelectionOrder 1;
		Weapon.Kickback 1;
		Weapon.BobRangeX 0.5;
		Weapon.BobSpeed 1.5;
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Ready:
			CALI A 1 {
				A_WeaponReady();
				if (GetCvar("SoulSurgeReady")) {
					return ResolveState("SoulCharge");
				}
				return ResolveState(null);
			}
			Loop;
		Deselect:
			CALI # 1 A_Lower(20);
			Loop;
		Select:
			CALI A 1 A_Raise(20);
			Loop;
		Fire:
			/* passthrough */
		Slash1:
			TNT1 A 0 {
				if (CountInv("PowerSuper") || CountInv("PowerHyper")) {
					A_PlaySound("sweapons/soulsurge", 6, 0.5);
					if (CountInv("PowerHyper")) A_FireProjectile("HyperEnergyBladeLeft", 0, 0);
					else {
						A_FireProjectile("EnergyBladeLeft", 0, 0);
						A_TakeInventory("ChaosEnergy", 4);
					}
				}
			}
			CAL1 A 1;
			TNT1 A 0 A_StartSound("sweapons/swordswing", 9);
			CAL1 BCD 1 A_CustomPunch(25, CPF_NOTURN, 0, "InvisiblePuff", 90, 0, 0, "ArmorBonus", "sweapons/finalhit");
			TNT1 A 0 A_CustomPunch(150, CPF_NOTURN, 0, "InvisiblePuff", 90, 0, 0, "ArmorBonus", "sweapons/finalhit");
			CAL1 EEEEE 1 {
				A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
				if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ATTACK && (!(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_ATTACK) || (CountInv("PowerSuper") || CountInv("PowerHyper")))) {
					return ResolveState("Slash2");
				}
				return ResolveState(null);
			}
			Goto Ready;
		Slash2:
			TNT1 A 0 {
				if (CountInv("PowerSuper") || CountInv("PowerHyper")) {
					A_PlaySound("sweapons/soulsurge", 6, 0.5);
					if (CountInv("PowerHyper")) A_FireProjectile("HyperEnergyBladeRight", 0, 0);
					else {
						A_FireProjectile("EnergyBladeRight", 0, 0);
						A_TakeInventory("ChaosEnergy", 4);
					}
				}
			}
			TNT1 A 0 A_StartSound("sweapons/swordswing", 9);
			CAL2 ABCD 1 A_CustomPunch(25, CPF_NOTURN, 0, "InvisiblePuff", 90, 0, 0, "ArmorBonus", "sweapons/finalhit");
			TNT1 A 0 A_CustomPunch(150, CPF_NOTURN, 0, "InvisiblePuff", 90, 0, 0, "ArmorBonus", "sweapons/finalhit");
			CAL2 EEEEE 1 {
				A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
				if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ATTACK && (!(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_ATTACK) || (CountInv("PowerSuper") || CountInv("PowerHyper")))) {
					return ResolveState("Slash1");
				}
				return ResolveState(null);
			}
			Goto Ready;
		AltFire:
			TNT1 A 0 A_Overlay(-3, "BoostStart");
			Goto Ready;
		SoulCharge:
			SOUS AAABBBCCCDDDEEEFFFGGG 1 {
				DamageFactor = 0;
				if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ATTACK) {
					cvar.FindCvar("SoulSurgeReady").SetBool(false);
					return ResolveState("SoulChargeFailed");
				}
				return ResolveState(null);
			}
			Goto SoulCharged;
		SoulCharged:
			TNT1 A 0 A_JumpIf(!GetCvar("SoulSurgeReady"), "SoulChargeFailed");
			SOUS HHHHHHHHHHH 1 {
				if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ATTACK) {
					return ResolveState("SoulSurge");
				}
				A_SetMugshotState("Rampage");
				cvar.FindCvar("SoulSurgeReady").SetBool(false);
				return ResolveState(null);
			}
			Goto SoulChargeFailed;
		SoulSurge:
			CAL1 E 15 {
				A_CustomPunch(4000, CPF_NOTURN, 0, "InvisiblePuff", 128, 0, 0, "ArmorBonus", "sweapons/finalhit");
				A_Explode(500, 256, XF_NOTMISSILE|XF_THRUSTZ);
				if (!(CountInv("PowerSuper") || CountInv("PowerHyper"))) {
					A_TakeInventory("ChaosEnergy", 400);
				}
				A_StartSound("sweapons/soulsurge", 8);
				A_ChangeVelocity(128, 0, 0, CVF_Relative|CVF_Replace);
				A_GiveInventory("Health", 100);
				cvar.FindCvar("SoulSurgeReady").SetBool(false);
			}
			CALI DCBA 1 {
				DamageFactor = 1;
			}
			Goto Ready;
		SoulChargeFailed:
			SOUS GFEDCBA 3;
			TNT1 A 0 { DamageFactor = 1; }
			Goto Ready;
		Spawn:
			CBRN A -1;
			Stop;
	}
}

class AcceleratedPistol : LockLoadWeapon
{
	int charge;
	action void AccelFire()
	{
		A_PlaySound("sweapons/pistol", 1);
		if (CountInv("PowerSuper")||CountInv("PowerHyper")) {
			A_FireBullets(0, 0, 1, 30, "NoGibPuff");
		}
		else {
			A_FireBullets(2, 2, -1, 15, "NoGibPuff");
		}
		A_GunFlash();
	}
	action void RecoilBurst()
	{
		int strengthbonus, recoilbonus, ammopenalty;
		if (invoker.charge) {
			strengthbonus = invoker.charge * 15;
			recoilbonus = invoker.charge / 4;
			ammopenalty = invoker.charge / 6;
			invoker.AmmoUse2 = 8 + ammopenalty;
		}
		A_StopSound(11);
		A_StartSound("weapons/rbeam", 1);
		if (CountInv("PowerHyper")) {
			A_RailAttack(160 + strengthbonus, 0, 1, "06b400", "fdff00", RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 25, 1.0, 1.0);
			A_ChangeVelocity (Cos(Pitch) * (-10 - recoilbonus), 0, Sin(Pitch) * (10 + recoilbonus), CVF_Relative);
		}
		else if (invoker.charge >= 50) {
			A_RailAttack(90 + strengthbonus, 0, 1, "009c9f", "6364bb", RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 25, 1.0, 1.0);
			A_ChangeVelocity (Cos(Pitch) * (-10 - recoilbonus), 0, Sin(Pitch) * (10 + recoilbonus), CVF_Relative);
		}
		else {
			A_RailAttack(80 + strengthbonus, 0, 1, "c5feff", "c5c6ff", RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 25, 1.0, 1.0);
			A_ChangeVelocity (Cos(Pitch) * (-7 - recoilbonus), 0, Sin(Pitch) * (7 + recoilbonus), CVF_Relative);
		}
		A_GunFlash("BurstFlash");
		invoker.charge = 0;
		invoker.AmmoUse2 = 8;
	}
	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.Kickback 1;
		Weapon.AmmoType1 "ChaosEnergy";
		Weapon.AmmoType2 "ChaosEnergy";
		Weapon.AmmoUse1 2;
		Weapon.AmmoUse2 8;
		Weapon.AmmoGive 60;
		Weapon.BobSpeed 1.5;
		Inventory.PickupMessage "Accelerated Pistol";
		Tag "Accelerated Pistol";
		+FORCEPAIN
		+PUFFONACTORS
		+ALWAYSPUFF
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Ready:
			SPIS A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM);
			Loop;
		Deselect:
			SPIS # 1 A_Lower(20);
			Loop;
		Select:
			SPIS A 1 A_Raise(20);
			Loop;
		Fire:
			SPIS AAA 2 {
				AccelFire();
				A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			}
			SPIS BC 1 A_WeaponReady(WRF_NOBOB);
			Goto Ready;
		AltFire:
			SPIS B 1 {
				if (GetPlayerInput(INPUT_BUTTONS) & BT_ALTATTACK && GetPlayerInput(INPUT_OLDBUTTONS) & BT_ALTATTACK) {
					if (invoker.charge == 1) A_StartSound("sweapons/recoilcharge", 1);
					if (invoker.charge > 1 || !GetCvar("RecoilBurstRelease")) A_ZoomFactor(1.2);
					if (invoker.charge < 50) {
						invoker.charge++;
						A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH);
					}
					else {
						A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
					}
					if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ATTACK) return ResolveState("RecoilBurst");
				}
				return ResolveState(null);
			}
			TNT1 A 0 A_ReFire("RecoilCharge");
			TNT1 A 0 A_JumpIf(GetCvar("RecoilBurstRelease"), "RecoilBurst");
			TNT1 A 0 {
				A_ZoomFactor(1);
				A_StopSound(1);
				invoker.charge = 0;
			}
			Goto Ready;
		RecoilBurst:
			TNT1 A 0 A_ZoomFactor(1);
			SPIS B 1 RecoilBurst();
			SPIS BBBBBBCDE 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			SPIS EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEDCB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			Goto Ready;
		Flash:
			SPIF A 1 Bright A_Light1;
			Goto LightDone;
		BurstFlash:
			SPIF B 4 Bright A_Light1;
			Goto LightDone;
		Spawn:
			PIST A -1;
			Stop;
	}
}

class RapidShotty : LockLoadWeapon
{
	action void RapidFire()
	{
		A_PlaySound("sweapons/shardshot", 1);
		A_FireBullets(18, 10, 10, 7, "BulletPuff");
		if (CountInv("PowerSuper")||CountInv("PowerHyper")) {
			A_GunFlash("SuperFlash");
		}
		else {
			A_GunFlash();
		}
	}
	action void ForceFire(int intensity)
	{
        if (!CountInv("PowerSuper") && !CountInv("PowerHyper")) {
			for (int x=1; x<intensity; x++) {
				A_TakeInventory("ChaosEnergy", 24, AAPTR_Player1);
			}
        }
		A_PlaySound("sweapons/forceshot", 1);
		A_ChangeVelocity(Cos(Pitch) * -10, 0, Sin(Pitch) * 10, CVF_Relative);
		A_FireBullets(4, 2, 25, 2 * intensity, "BulletPuff");
		A_GunFlash("ForceFlash");
	}
	Default
	{
		Weapon.SelectionOrder 1300;
		Weapon.AmmoType1 "ChaosEnergy";
		Weapon.AmmoType2 "ChaosEnergy";
		Weapon.AmmoUse1 12;
		Weapon.AmmoUse2 20;
		Weapon.AmmoGive 64;
		Weapon.Kickback 1000;
		Weapon.BobSpeed 1.5;
		Inventory.PickupMessage "Rapid Shotty";
		Tag "Rapid Shotty";
		+PUFFONACTORS
		+ALWAYSPUFF
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Ready:
			SCTG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			SCTG # 1 {
				A_Lower(20);
				A_StopSound(6);
			}
			Loop;
		Select:
			SCTG A 1 A_Raise(20);
			Loop;
		Fire:
			TNT1 A 0 A_JumpIf(CountInv("PowerHyper"), "SuperFire");
			SCTG B 1 RapidFire();
			SCTG BBBBB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			SCTG CCC 1 A_WeaponReady(WRF_NOBOB);
			SCTG CCCCCCC 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			SCTG A 0 A_ReFire();
			Goto Ready;
		AltFire:
			SCTG B 1 ForceFire(2);
			SCTG BBBBB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			TNT1 A 0 { if (CountInv("PowerHyper")) { A_ReFire(); } }
			SCTG BBBBB 1 {
                if (GetPlayerInput(MODINPUT_BUTTONS) & BT_ALTATTACK && !(GetPlayerInput(MODINPUT_OLDBUTTONS) & BT_ALTATTACK)) {
                    return ResolveState("AltFireAgain");
                }
                A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
                return ResolveState(null);
            }
			TNT1 A 0 { if (CountInv("PowerHyper")) A_ReFire(); }
			SCTG CDE 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
            Goto ForceReload;
		AltFireAgain:
			SCTG B 1 ForceFire(1);
			SCTG BBBBBBBBBBCDE 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
            Goto ForceReload;
        ForceReload:
			SCTG F 1 A_PlaySound("sweapons/forcereload", 6);
			SCTG FFFFFFFFFFFFFFFFFFFFFFFFFFFEDCAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			SCTG A 0 A_ReFire();
			Goto Ready;
		SuperFire:
			SCTG B 3 RapidFire();
			SCTG C 2;
			SCTG C 3;
			SCTG A 0 A_ReFire();
			Goto Ready;
		Flash:
			SCTF A 4 Bright;
			Stop;
		SuperFlash:
			SCTF A 2 Bright;
			Stop;
		ForceFlash:
			SCTF B 4 Bright;
			Stop;
		Spawn:
			SCAT A -1;
			Stop;
	}
}

class ShurikenCrossbow : LockLoadWeapon
{
	Default
	{
		Weapon.SelectionOrder 800;
		Weapon.AmmoUse 32;
		Weapon.AmmoGive 100;
		Weapon.Kickback 1;
		Weapon.BobSpeed 1.5;
		Weapon.AmmoType "ChaosEnergy";
		Inventory.PickupMessage "Shuriken Crossbow";
		Tag "Shuriken Crossbow";
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Ready:
			SHRL A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			SHRL # 1 A_Lower(20);
			Loop;
		Select:
			SHRL A 1 A_Raise(20);
			Loop;
		Fire:
			SHRL B 1 A_FireProjectile("Shuriken", 0, 1);
			TNT1 A 0 { if (CountInv("PowerHyper")) { A_ReFire(); } }
			SHRL BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBCDEFG 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			TNT1 A 0 A_ReFire();
			Goto Ready;
		Spawn:
			SURI A -1;
			Stop;
	}
}

class EclipseBlaster : LockLoadWeapon
{
	action void EclipseFire()
	{
		if (CountInv("PowerHyper")) {
			A_FireProjectile("ChaosEnergyBallEX", 0, 0);
		}
		else {
			A_FireProjectile("ChaosEnergyBall", 0, 1);
		}
	}
	Default
	{
		Weapon.SelectionOrder 1400;
		Weapon.AmmoType1 "ChaosEnergy";
		Weapon.AmmoType2 "ChaosEnergy";
		Weapon.AmmoUse 5;
		Weapon.AmmoGive 35;
		Weapon.KickBack 1000;
		Weapon.BobSpeed 1.5;
		+EXTREMEDEATH
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "Eclipse Blaster Mk. V";
		Tag "Eclipse Blaster Mk. V";
	}
	States
	{
		Ready:
			ECBL A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			ECBL # 1 A_Lower(20);
			Loop;
		Select:
			ECBL A 1 A_Raise(20);
			Loop;
		Fire:
			ECBL BBBBBBB 1 EclipseFire();
			ECBL DDDDDDDDDDDDDDDDDDDDDDD 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			TNT1 A 0 A_ReFire();
			Goto Ready;
		AltFire:
			ECBL C 1 {
				int ringLimit = 300;
				if ((Health < ringLimit) && CountInv("ChaosEnergy") && !(CountInv("PowerSuper") || CountInv("PowerHyper"))) {
					A_PlaySound("sweapons/healthcharge", 4096);
					A_TakeInventory("ChaosEnergy", 16);
					A_GiveInventory("Health", 6);
					if (Health > ringLimit) { A_SetHealth(ringLimit); }
				}
				else {
					A_PlaySound("sglobal/error", 4096);
					return ResolveState("EndHeal");
				}
				return ResolveState(null);
			}
			TNT1 A 0 A_ReFire();
			/* passthrough */
		EndHeal:
			ECBL DDDDDDDDDDDDDDD 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			Goto Ready;
		Spawn:
			ECLI A -1;
			Stop;
	}
}

class AirStrikeLauncher : LockLoadWeapon
{
	Default
	{
		Weapon.SelectionOrder 1500;
		Weapon.AmmoUse 40;
		Weapon.AmmoGive 200;
		Weapon.AmmoType "ChaosEnergy";
		Inventory.PickupMessage "Rocket Rioter";
		Tag "Rocket Rioter";
		Weapon.BobSpeed 1.5;
		Weapon.Kickback 1000;
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOAUTOAIM
		+WEAPON.ALT_AMMO_OPTIONAL
	}
	action void FireRocket(int Amount)
	{
		if (CountInv("PowerHyper")) {
			int SpreadX, SpreadY, Iterator;
			for (Iterator = 1; Iterator <= Amount; Iterator++) {
				if (A_CheckFloor("Null")) {
					SpreadX = random(-30, 30);
					SpreadY = random(-30, 30);
					A_FireProjectile("ChaosRocket", 0, 0, SpreadX, SpreadY);
				} else {
					SpreadX = random(-60, 60);
					SpreadY = random(-60, 60);
					A_FireProjectile("AirstrikeRocket", 0, 0, SpreadX, SpreadY);
					A_ChangeVelocity(Cos(Pitch) * -10, 0, Sin(Pitch) * 10, CVF_Relative);
				}
			}
		} else {
			if (A_CheckFloor("Null")) {
				A_FireProjectile("ChaosRocket", 0, 1);
			} else {
				A_FireProjectile("AirstrikeRocket", 0, 1, 0, Vel.Z * 2);
				A_ChangeVelocity(Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative);
			}
		}
	}
	States
	{
		Ready:
			MISG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
			Loop;
		Deselect:
			MISG # 1 A_Lower(20);
			Loop;
		Select:
			MISG A 1 A_Raise(20);
			Loop;
		Fire:
			TNT1 A 0 A_TakeInventory("KickCooldown", 3);
			TNT1 A 0 FireRocket(7);
			MISG ABBBBB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
			Goto Ready;
		AltFire:
			MISG A 1 A_GiveInventory("RemoteDet");
			MISG B 1 A_ReFire();
			MISG A 1 A_TakeInventory("RemoteDet");
			Goto Ready;
		Spawn:
			LAUN A -1;
			Stop;
	}
}

class SonicKick : CustomInventory
{
	int inuse;
	int EXKick;
	bool DidTarget;
	action void HomeInOnTarget()
	{
		invoker.DidTarget = false;
		FTranslatedLineTarget ltinfo;
		bool ltattack = LineAttack(angle, 896, pitch, 0, 'None', "InvisiblePuff", LAF_NORANDOMPUFFZ, ltinfo, 0);
		let ltarget = ltinfo.linetarget;
		if (ltarget && Distance3D(ltarget) > 128 && health > 0) {
			invoker.DidTarget = true;
			A_Stop();
			double ltdistance = Distance2D(ltarget);
			double zoffset;
			if (pitch <= -55) {
				A_SetPitch(-55);
				zoffset = -65;
			}
			else if (pitch < 0) zoffset = pitch - 10;
			else if (pitch < 7.0) zoffset = -10;
			else if (pitch >= 40) {
				A_SetPitch(40);
				zoffset = 30;
			}
			else zoffset = 1.2;
			A_SetAngle(ltinfo.angleFromSource);
			double oriX = (-40 - ltarget.radius) * cos(angle) - 0 * sin(angle);
			double oriY = (-40 - ltarget.radius) * sin(angle) + 0 * cos(angle);
			SetOrigin(ltarget.Vec3Offset(oriX, oriY, zoffset), false);
			A_StartSound("sweapons/sdash", 8);
		}
	}
	States
	{
		Use:
			TNT1 A 0 A_Overlay(-2, "FloorCheck");
			TNT1 A 0 A_OverlayOffset(-2, 0, 32);
			Fail;
		FloorCheck:
			TNT1 A 0 { invoker.EXKick = 0; }
			TNT1 A 0 A_CheckFloor("KickAttack");
			TNT1 A 0 { invoker.EXKick = 1; }
		KickAttack:
			TNT1 A 0 HomeInOnTarget();
			TNT1 A 0 A_JumpIfInventory("PowerSuper", 1, "SuperKickAttack");
			TNT1 A 0 A_JumpIfInventory("PowerHyper", 1, "HyperKickAttack");
			TNT1 A 0 A_AlertMonsters();
			TNT1 A 0 A_PlaySound("sonic/grunt", 8);
			TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SonicEagle");
			KICK A 1 A_PlaySound("sweapons/swordswing", 7);
			KICK BC 1;
			KICK D 1 A_CustomPunch(random(1, 4) * 48, true, CPF_NOTURN, "SonicKickPuff", 120, 0, 0, "ArmorBonus", "sweapons/finalhit");
			KICK EF 1;
			Stop;
		SonicEagle:
			EAGL A 1 A_PlaySound("sweapons/swordswing", 7);
			EAGL BC 1 { if (invoker.DidTarget) A_Stop(); }
			EAGL D 1 A_CustomPunch(random(1, 4) * 96, true, CPF_NOTURN, "SonicKickPuff", 160, 0, 0, "ArmorBonus", "sweapons/finalhit");
			EAGL E 1;
			Stop;
		SuperKickAttack:
			TNT1 A 0 A_GiveInventory("KickCooldown", 25);
			TNT1 A 0 A_AlertMonsters();
			TNT1 A 0 A_PlaySound("sonic/grunt", 8);
			TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "SuperSonicEagle");
			SKIK A 1 A_PlaySound("sweapons/swordswing", 7);
			SKIK BC 1;
			SKIK D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 120, 0, 0, "ArmorBonus", "sweapons/finalhit");
			SKIK E 1;
			Stop;
		SuperSonicEagle:
			SEAG A 1 A_PlaySound("sweapons/swordswing", 7);
			SEAG BC 1 { if (invoker.DidTarget) A_Stop(); }
			SEAG D 1 A_CustomPunch(random(1,16)*96, true, CPF_NOTURN, "SonicKickPuff", 160, 0, 0, "ArmorBonus", "sweapons/finalhit");
			SEAG E 1;
			Stop;
		HyperKickAttack:
			TNT1 A 0 A_AlertMonsters();
			TNT1 A 0 A_PlaySound("sonic/grunt", 8);
			TNT1 A 0 A_JumpIf(invoker.EXKick > 0, "HyperSonicEagle");
			HKIK A 1 {
				A_PlaySound("sweapons/shockwave", 7);
				A_FireBullets(25, 10, 75, 5, "GibPuff", 0);
			}
			HKIK BC 1;
			HKIK D 1 A_CustomPunch(random(1,16)*48, true, CPF_NOTURN, "SonicKickPuff", 120, 0, 0, "ArmorBonus", "sweapons/finalhit");
			HKIK EF 1;
			Stop;
		HyperSonicEagle:
			HEAG A 1 {
				A_PlaySound("sweapons/shockwave", 7);
				A_FireBullets(5, 5, 100, 10, "GibPuff", 0);
			}
			HEAG BC 1 { if (invoker.DidTarget) A_Stop(); }
			HEAG D 1 A_CustomPunch(random(1,16)*96, true, CPF_NOTURN, "SonicKickPuff", 160, 0, 0, "ArmorBonus", "sweapons/finalhit");
			HEAG E 1;
			Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (owner.Pos.Z < owner.GetZAt()) {
			owner.SetOrigin((owner.Pos.X, owner.Pos.Y, owner.GetZAt()), false);
		}
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&BT_RELOAD && !(owner.GetPlayerInput(MODINPUT_OLDBUTTONS)&BT_RELOAD) && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 6;
		}
	}
}

class HomingSnapQuickStep : CustomInventory
{
	int inuse;
	int counter;
	States
	{
		Use:
			TNT1 A 0 A_Overlay(10, "Startup");
			TNT1 A 0 A_OverlayOffset(10, 0, 32);
			Fail;
		Startup:
			TNT1 A 1 {
				if (player.onground) {
					/* Quick Step */
					int input = 0;
					int QuickStepSpeed = 35;
					A_StartSound("sonic/quickstep", 10);
					DamageFactor = 0;
					int forward = GetPlayerInput(INPUT_FORWARDMOVE);
					int sidem = GetPlayerInput(INPUT_SIDEMOVE);
					if (forward > 0) A_ChangeVelocity(QuickStepSpeed, 0, 0, CVF_Relative);
					if (sidem < 0) A_ChangeVelocity(0, QuickStepSpeed, 0, CVF_Relative);
					if (sidem > 0) A_ChangeVelocity(0, QuickStepSpeed * -1, 0, CVF_Relative);
					if (forward < 0 || (forward == 0 && sidem == 0)) {
						A_ChangeVelocity(QuickStepSpeed * -1, 0, 0, CVF_Relative);
					}
					return ResolveState("QuickStepEnd");
				}
				else {
					/* Homing Snap */
					FTranslatedLineTarget ltinfo;
					bool ltattack = LineAttack(angle, 896, pitch, 0, 'None', "InvisiblePuff", LAF_NORANDOMPUFFZ, ltinfo, 0);
					let ltarget = ltinfo.linetarget;
					if (ltarget && Distance3D(ltarget) > 128 && health > 0) {
						double ltdistance = Distance2D(ltarget);
						double oriX = -120 * cos(angle) - 0 * sin(angle);
						double oriY = -120 * sin(angle) + 0 * cos(angle);
						SetOrigin(ltarget.Vec3Offset(oriX, oriY, 1.2), false);
						A_SetPitch(12);
						A_SetAngle(ltinfo.angleFromSource);
						if (player.ReadyWeapon.GetClassName() == "Caliburn" && ltarget.bIsMonster && (CountInv("ChaosEnergy") >= 400 || CountInv("PowerSuper") || CountInv("PowerHyper"))) {
							A_StartSound("sweapons/soulcharge", 8);
							A_StartSound("sonic/soulcharge", CHAN_BODY);
							cvar.FindCvar("SoulSurgeReady").SetBool(true);
						}
						else {
							A_StartSound("sweapons/sdash", 8);
						}
					}
					else {
						return ResolveState("Null");
					}
					return ResolveState("DashEnd");
				}
				return ResolveState(null);
			}
			TNT1 A 4;
			TNT1 A 0 A_Stop();
			TNT1 A 5;
			Stop;
		DashEnd:
			TNT1 A 1 {
				invoker.counter = 0;
			}
			/* passthrough */
		HomingFloat:
			TNT1 A 1 {
				A_Stop();
				int buttons = GetPlayerInput(MODINPUT_BUTTONS);
				bool enemyVisible = CheckLOF(CLOFF_JUMPENEMY|CLOFF_SKIPOBJECT|CLOFF_MUSTBESHOOTABLE|CLOFF_JUMPNONHOSTILE, 128);
				invoker.counter++;
				if (invoker.counter > 30 || !enemyVisible || buttons & (BT_CROUCH|BT_ATTACK|BT_ALTATTACK|BT_RELOAD)) {
					return ResolveState("HomingEnd");
				}
				if (buttons & BT_JUMP) {
					A_ChangeVelocity(0, 0, 15, CVF_Replace);
					A_StartSound("*doublejump", CHAN_BODY);
					return ResolveState("HomingEnd");
				}
				return ResolveState(null);
			}
			Loop;
		HomingEnd:
			TNT1 A 0 {
				cvar.FindCvar("SoulSurgeReady").SetBool(false);
			}
			Stop;
		QuickStepEnd:
			TNT1 A 5;
			TNT1 A 5 { if (!GetCvar("DidJump")) A_Stop(); }
			TNT1 A 0 { DamageFactor = 1; }
			Stop;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if (inuse > 0)
		{
			inuse--;
		}
		if (owner is "PlayerPawn" && owner.GetPlayerInput(MODINPUT_BUTTONS)&(BT_ZOOM|BT_SPEED) && !(owner.GetPlayerInput(MODINPUT_OLDBUTTONS)&(BT_ZOOM|BT_SPEED)) && inuse == 0)
		{
			owner.UseInventory(self);
			inuse = 10;
		}
	}
}

