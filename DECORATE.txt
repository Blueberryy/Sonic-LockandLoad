#include "actors/ammo.txt"
#include "actors/hints.txt"
#include "actors/replacements.txt"
#include "actors/weapons.txt"
#include "actors/pickups.txt"
#include "actors/enemies.txt"

ACTOR SonicPlayer : PlayerPawn
{
  Speed 2
  Health 125
  Radius 10
  Height 46
  PainChance 255
  Player.MaxHealth 999
  Player.DisplayName "Sonic"
  Player.JumpZ 10.0
  Player.StartItem "Unarmed"
  Player.StartItem "Speedforce Railgun"
  Player.StartItem "ChaosEnergy", 30
  States
  {
  Spawn:
    PLAY A -1
    Loop
  See:
    PLAY ABCD 4 
    Loop
  Missile:
    PLAY E 12
    Goto Spawn
  Melee:
    PLAY F 6 BRIGHT
    Goto Missile
  Pain:
    PLAY G 4 
    PLAY G 4 A_Pain
    Goto Spawn
  Death:
    PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
  Death1:
    PLAY H 10
    PLAY I 10 A_PlaySound("sonicdoom/thatwaslame")
    PLAY J 10 A_NoBlocking
    PLAY KLM 10
    PLAY N -1
    Stop
  XDeath:
    PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
  XDeath1:
    PLAY O 5
    PLAY P 5 A_PlaySound("sonicdoom/ohno")
    PLAY Q 5 A_NoBlocking
    PLAY RSTUV 5
    PLAY W -1
    Stop
  AltSkinDeath:
    PLAY H 6
    PLAY I 6 A_PlaySound("sonicdoom/thatwaslame")
    PLAY JK 6
    PLAY L 6 A_NoBlocking
    PLAY MNO 6
    PLAY P -1
    Stop
  AltSkinXDeath:
    PLAY Q 5 A_PlaySound("sonicdoom/ohno")
    PLAY R 0 A_NoBlocking
    PLAY R 5 A_SkullPop
    PLAY STUVWX 5
    PLAY Y -1
    Stop
  }
}