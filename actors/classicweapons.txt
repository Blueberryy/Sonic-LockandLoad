ACTOR SonicFist : Weapon replaces Fist 1212
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 10
  AttackSound "sonicdoom/finalhit"
  Tag "Sonic's Fist"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    Loop
  Deselect:
    PUNG A 1 A_Lower
    Loop
  Select:
    PUNG A 1 A_Raise
    Loop
  Fire:
    PUNG B 1
    PUNG C 1
	{
		if (CountInv("PowerSuper") < 1)
		{
			A_CustomPunch(4, false, 0, "InvisiblePuff");
		}
		if (CountInv("PowerSuper") > 0)
		{
			A_CustomPunch(40, false);
		}
	}
    PUNG D 1
    PUNG C 1
    PUNG B 1 A_ReFire
    Goto Ready
  AltFire:
    TNT1 A 0 A_PlaySound("sonicdoom/drift", 2)
    TNT1 A 0 A_Recoil(-6)
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_GiveInventory("ChaosEnergy")
    SPIN A 1 A_CustomPunch(2)
    TNT1 A 0 A_ReFire
    TNT1 A 0 A_StopSound(2)
	TNT1 A 0 A_Light(0)
	Goto Ready
  }
}

ACTOR ChaosBlaster : Weapon
{
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 2
  Weapon.AmmoGive 25
  Weapon.KickBack 100
  Weapon.AmmoType "ChaosEnergy"
  Tag "Chaos Blaster"
  Inventory.PickupMessage "Picked up the Chaos energy blaster!"
  States
  {
  Ready:
    PLSG A 1 A_WeaponReady
    Loop
  Deselect:
    PLSG A 1 A_Lower
    Loop
  Select:
    PLSG A 1 A_Raise
    Loop
  Fire:
    PLSG A 0 A_FirePlasma
	PLSG A 1
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 2);
		}
	}
    Goto Ready
  Flash:
    PLSF A 4 Bright A_Light1
    Goto LightDone
    PLSF B 4 Bright A_Light1
    Goto LightDone
  Spawn:
    PLAS A -1
    Stop
  }
}
ACTOR SpeedyShotgun : Weapon
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoUse1 16
  Weapon.AmmoUse2 8
  Weapon.AmmoGive 32
  Weapon.KickBack 500
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  AttackSound "weapons/shotgf"
  Tag "Speedy Shotgun"
  Inventory.PickupMessage "Picked up the speedy shotgun!"
  States
  {
  Ready:
    SHTG A 1 A_WeaponReady
    Loop
  Deselect:
    SHTG A 1 A_Lower
    Loop
  Select:
    SHTG A 1 A_Raise
    Loop
  Fire:
    SHTG A 2
	SHTG A 0 A_FireBullets(5.6, 0, 7, 3, "BulletPuff")
    SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
    SHTG A 3 A_GunFlash
    Goto Ready
  AltFire:
	SHTG A 0 A_ChangeVelocity (Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative)
    SHTG A 0 A_FireBullets (8, 3, 10, 4, "BulletPuff")
    SHTG A 3 A_GunFlash("QuickFlash")
	SHTG A 0 A_ChangeVelocity (Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative)
    SHTG A 0 A_FireBullets (5, 2, 10, 5, "BulletPuff")
    SHTG A 3 A_GunFlash("QuickFlash")
	SHTG A 0 A_ChangeVelocity (Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative)
    SHTG A 0 A_FireBullets (3, 1, 10, 6, "BulletPuff")
    SHTG A 3 A_GunFlash("QuickFlash")
	SHTG A 0 A_ChangeVelocity (Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative)
    SHTG A 0 A_FireBullets (1, 0, 10, 7, "BulletPuff")
    SHTG A 3 A_GunFlash("QuickFlash")
	SHTG A 0 A_ChangeVelocity (Cos(Pitch) * -5, 0, Sin(Pitch) * 5, CVF_Relative)
    SHTG A 0 A_FireBullets (0, 0, 10, 8, "BulletPuff")
    SHTG A 7 A_GunFlash
    SHTG BC 5
    SHTG D 4
    SHTG CB 5
    SHTG A 3
    SHTG A 7 A_ReFire
    Goto Ready
  Flash:
    SHTF A 4 Bright A_Light1
    SHTF B 3 Bright A_Light2
    Goto LightDone
  QuickFlash:
    SHTF A 1 Bright A_Light1
    SHTF B 1 Bright A_Light2
    Goto LightDone
  Spawn:
    SHOT A -1
    Stop
  }
}

ACTOR ClassicRocketStream : Weapon
{
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 15
  Weapon.AmmoGive 45
  Weapon.AmmoType "ChaosEnergy"
  Tag "Rocket Stream"
  Inventory.PickupMessage "Picked up the rocket stream launcher!"
  States
  {
  Ready:
    MISG A 1 A_WeaponReady
    Loop
  Deselect:
    MISG A 1 A_Lower
    Loop
  Select:
    MISG A 1 A_Raise
    Loop
  Fire:
    MISG B 1 A_GunFlash
    MISG B 0 A_FireProjectile("StreamRocket")
	MISG A 1
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 15);
		}
	} 
    Goto Ready
  Flash:
    MISF A 3 Bright A_Light1
    MISF B 4 Bright
    MISF CD 4 Bright A_Light2
    Goto LightDone
  Spawn:
    LAUN A -1
    Stop
  }
}
ACTOR ChaosRain : Weapon
{
   Weapon.SelectionOrder 700
   Weapon.AmmoUse 1
   Weapon.AmmoGive 20
   Weapon.AmmoType "ChaosEnergy"
   Tag "Chaos Rain"
   Inventory.PickupMessage "Picked up the Chaos Rain cannon!"
   States
   {
   Ready:
     CHGG A 1 A_WeaponReady
     Loop
   Deselect:
     CHGG A 1 A_Lower
     Loop
   Select:
     CHGG A 1 A_Raise
     Loop
   Fire:
     CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
     CHGG A 0 A_GunFlash
     CHGG A 0 A_FireBullets(5.6, 0, 1, 1, "BulletPuff")
	 CHGG A 1
	 {
		 if (CountInv("PowerSuper") > 0)
		 {
			 A_GiveInventory("ChaosEnergy", 1);
		 }
	 }
     CHGG B 0 A_FireBullets(5.6, 0, 1, 1, "BulletPuff")
	 CHGG B 1
	 {
		 if (CountInv("PowerSuper") > 0)
		 {
			 A_GiveInventory("ChaosEnergy", 1);
		 }
	 }
     CHGG B 0 A_ReFire
     Goto Ready
   Spawn:
     MGUN A -1
     Stop
   }
}