ACTOR Ring : CustomInventory replaces HealthBonus
{
  Inventory.PickupMessage "Picked up a ring."
  Inventory.PickupSound "spickup/ring"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    RIN1 A -1
    Stop
  Pickup:
    TNT1 A 0
	{
		if (CountInv("AmyCheck") > 0)
		{
			if (CountInv("PowerSuper") > 0)
			{
				A_GiveInventory("ChaosEnergy", 1);
			}
			else
			{
				A_GiveInventory("Health", 1);
			}
		}
		else
		{
			A_GiveInventory("Health", 1);
		}
	}
	Stop
  }
}
ACTOR RingBox : CustomInventory replaces Stimpack
{
  Health 1
  Radius 20
  Height 30
  Inventory.PickupMessage ""
  DeathSound "spickup/boxdestroy"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  +SHOOTABLE
  +NOBLOOD
  States
  {
  Spawn:
    RBOX A -1
    Stop
  Pickup:
  Death:
    TNT1 A 0 A_DropItem("10Rings")
	TNT1 A 0 A_Scream
	Stop
  }
}
ACTOR 10Rings : CustomInventory
{
  Inventory.PickupMessage "Picked up 10 rings."
  Inventory.PickupSound "spickup/10rings"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    10RI ABCDEFGH 2
	10RI I -1
    Stop
  Pickup:
    TNT1 A 0
	{
		if (CountInv("AmyCheck") > 0)
		{
			if (CountInv("PowerSuper") > 0)
			{
				A_GiveInventory("ChaosEnergy", 10);
			}
			else
			{
				A_GiveInventory("Health", 10);
			}
		}
		else
		{
			A_GiveInventory("Health", 10);
		}
	}
	Stop
  }
}
ACTOR SpecialRing : CustomInventory replaces Medikit
{
  Inventory.PickupMessage "Picked up a Special ring."
  Inventory.PickupSound "spickup/specialring"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    RIN2 A -1
    Stop
  Pickup:
    TNT1 A 0
	{
		if (CountInv("AmyCheck") > 0)
		{
			if (CountInv("PowerSuper") > 0)
			{
				A_GiveInventory("ChaosEnergy", 50);
			}
			else
			{
				A_GiveInventory("ChaosEnergy", 50);
				if (Health < 300)
				{
					A_GiveInventory("Health", 50);
					if (Health > 300)
					{
						A_SetHealth(300);
					}
				}
			}
		}
		else
		{
			A_GiveInventory("ChaosEnergy", 50);
			if (Health < 300)
			{
				A_GiveInventory("Health", 50);
				if (Health > 300)
				{
					A_SetHealth(300);
				}
			}
		}
	}
	Stop
  }
}
ACTOR ChaosRing : CustomInventory replaces ArmorBonus
{
  Inventory.PickupMessage "Picked up a Chaos ring."
  Inventory.PickupSound "spickup/ring"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    RIN3 A -1
    Stop
  Pickup:
    TNT1 A 5
	{
		if (CountInv("AmyCheck") > 0)
		{
			if (CountInv("PowerSuper") > 0)
			{
				A_GiveInventory("ChaosEnergy", 6);
			}
			else
			{
				A_GiveInventory("Health", 5);
				A_GiveInventory("ChaosEnergy", 1);
			}
		}
		else
		{
			A_GiveInventory("Health", 5);
			A_GiveInventory("ChaosEnergy", 1);
		}
	}
	Stop
  } 
}
ACTOR SoulRing : CustomInventory
{
  Inventory.PickupMessage "Picked up a Soul ring."
  Inventory.PickupSound "spickup/ring"
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    RIN4 A -1
    Stop
  Pickup:
    TNT1 A 5 A_GiveInventory("ChaosEnergy", 50)
	Stop
  }
}

ACTOR SoulRecharge : CustomInventory
{
  Inventory.PickupMessage "Picked up a box of Soul rings!"
  Inventory.PickupSound "spickup/specialring"
  States
  {
  Spawn:
    PSTR A -1
    Stop
  Pickup:
	TNT1 A 0 A_GiveInventory("ChaosEnergy", 200)
	Stop
  }
}
ACTOR EmeraldRecharge : CustomInventory
{
  Inventory.PickupMessage "Picked up an Emerald recharge!"
  Inventory.PickupSound "spickup/specialring"
  States
  {
  Spawn:
    CPAK A -1
    Stop
  Pickup:
	TNT1 A 0 A_GiveInventory("ChaosEnergy", 500)
	Stop
  }
}
ACTOR InvincibilityBox : CustomInventory
{
  Inventory.PickupMessage "Invincibility!"
  Inventory.PickupSound "spickup/boxdestroy"
  +COUNTITEM
  States
  {
  Spawn:
    INVC A -1
    Stop
  Pickup:
	TNT1 A 0 A_GiveInventory("Invincibility", 1)
	TNT1 A 0 ACS_NamedExecute("Invincibility")
	Stop
  }
}
ACTOR ShadowFever : CustomInventory
{
  Inventory.PickupMessage "Shadow Fever!"
  Inventory.PickupSound "spickup/boxdestroy"
  +COUNTITEM
  States
  {
  Spawn:
    SHAF A -1
    Stop
  Pickup:
	TNT1 A 0 A_GiveInventory("Invincibility", 1)
	TNT1 A 0 ACS_NamedExecute("Invincibility")
	Stop
  }
}
ACTOR ChaosEmeralds : CustomInventory 
{
  Inventory.PickupMessage ""
  Inventory.PickupSound "spickup/chaosemerald"
  +COUNTITEM
  +INVENTORY.INVBAR
  States
  {
  Spawn:
    CHSM A -1
    Stop
  Pickup:
    TNT1 A 0 ACS_NamedExecute("ChaosEmeraldHandler")
	Stop
  Use:
    TNT1 A 0 A_TakeInventory("ChaosEmeralds", 1)
	TNT1 A 0
	{
		if (CountInv("AmyCheck") > 0)            // If playing as Amy...
		{
			A_SetHealth(1);                      // then set rings to 1.
		}
		else                                     // If not playing as Amy...
		{
			A_SetInventory("Health", 999);       // then set rings to maximum.
		}
		if (CountInv("ChaosEnergy") < 100)
		{
			A_SetInventory("ChaosEnergy", 100);
		}
	}
	TNT1 A 0 A_GiveInventory("SuperForm", 1)
	TNT1 A 0 A_GiveInventory("SuperInvincibility", 1)
	TNT1 A 0 A_PlaySound("sglobal/supersonic", 6)
	TNT1 A 0 A_PlaySound("sglobal/supertransform", 4)
	TNT1 A 0 
	{
	  if (CountInv("SonicCheck")) 
	  {
	    A_Log("\c[SonicBlue]Now I'll show you!!");
	  }
	  else if (CountInv("AmyCheck")) 
	  {
	    A_Log("\c[AmyPink]I'll destroy all of you!!");
	  }
	  else if (CountInv("ShadowCheck")) 
	  {
	    A_Log("\c[ShadowRedBlack]Witness the might of my power!!");
	  }
	  else if (CountInv("ClassicCheck")) 
	  {
	    A_Log("\c[SonicBlue]Let's blast through with SONIC SPEED!");
	  }
	}
	TNT1 A 0 ACS_NamedExecute("TransformSuper")
	TNT1 A 0 ACS_NamedExecuteAlways("TakeRingsIfSuper")
	Stop
  }
}

ACTOR ChaosCapsule : ChaosEnergy
{
  Inventory.PickupMessage "Picked up a Chaos Energy capsule."
  Inventory.Amount 20
  States
  {
  Spawn:
    CHSC A -1
	Stop
  }
}
ACTOR BlueChaosCapsule : ChaosEnergy
{
  Inventory.PickupMessage "Picked up a blue Chaos Energy capsule."
  Inventory.Amount 100
  States
  {
  Spawn:
    CHSC B -1
    Stop
  }
}
ACTOR RedChaosCapsule : ChaosEnergy
{
  Inventory.PickupMessage "Picked up a red Chaos Energy capsule."
  Inventory.Amount 400
  States
  {
  Spawn:
    CHSC C -1
    Stop
  }
}

ACTOR MacAndCheese : CustomInventory
{
  Inventory.PickupMessage "Picked up some mac and cheese."
  States
  {
  Spawn:
    FMAC A -1
    Stop
  Pickup:
    TNT1 A 0 A_Print("\c[ShadowRedBlack]I'm making mac and cheese,\n\c[ShadowRedBlack]and nobody can stop me!")
	TNT1 A 0 A_GiveInventory("Health", 100)
	TNT1 A 0 A_GiveInventory("ChaosEnergy", 100)
	Stop
  }
}