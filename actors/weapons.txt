ACTOR Unarmed : Weapon 1220
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 9999999999999
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 0.0
  Weapon.AmmoType "ChaosEnergy"
  Weapon.SlotNumber 1
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
    TNT1 A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	Goto Select
  Select:
	TNT1 A 1 A_Lower
	Goto Deselect
  Fire:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing")
    TNT1 A 0 A_PlaySound("sonicdoom/grunt")
    KICK BC 1
    KICK D 1 
	{
	  if (CountInv("PowerInvulnerable") < 1) 
	  {
	    A_CustomPunch(random(1,16)*10, true, CPF_NOTURN, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
	  }
	  else if (CountInv("PowerInvulnerable") > 0) 
	  {
	    A_CustomPunch(random(1,16)*100, true, CPF_NOTURN, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
	  }
	}
    KICK EFG 1
	TNT1 A 3 A_ReFire
    Goto Ready
  AltFire:
    TNT1 A 0
	{
	  if (CountInv("BoostCheck") < 1)
	  {
		A_GiveInventory("BoostCheck");
	    A_PlaySound("sonicdoom/boost", 2);
	  }
	}
    TNT1 A 0 A_PlaySound("sonicdoom/boosttrail", 1, 1.0, true)
	TNT1 A 0 A_LowGravity
    TNT1 A 0 A_Recoil(-15)
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_GiveInventory("ChaosEnergy", 1)
    BOOS A 1 
	{
	  if (CountInv("PowerInvulnerable") < 1) 
	  {
	    A_CustomPunch(1, false, CPF_NOTURN, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/hit");
	  }
	  else if (CountInv("PowerInvulnerable") > 0) 
	  {
	    A_CustomPunch(100, false, CPF_NOTURN, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/hit");
	  }
	}
	BOOS A 0 A_ReFire
	TNT1 A 0 A_Gravity
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  }
}

ACTOR Caliburn : Weapon replaces Chainsaw 3200
{
  Weapon.SelectionOrder 1
  Weapon.SlotNumber 1
  Weapon.Kickback 1000
  Weapon.AmmoType "Soul"
  Inventory.PickupMessage "Hey, it's Caliburn!"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    CALI A 1 A_WeaponReady
    Loop
  Deselect:
    CALI A 1 A_Lower
    Loop
  Select:
    CALI A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") < 1, "Swing")
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") > 0, "SuperSwing")
  Swing:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing")
    CALI B 1
	CALI A 0 A_Recoil(-8)
    CALI C 1 A_CustomPunch(6, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI D 1 A_CustomPunch(10, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI E 1 A_CustomPunch(14, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI F 1 A_CustomPunch(40, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI E 2 A_CustomPunch(12, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI D 2 A_CustomPunch(8, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI C 2 A_CustomPunch(4, CPF_PULLIN, 0, BulletPuff, 75, 0, 0, "ArmorBonus", "sonicdoom/hit")
    CALI B 1
    Goto Ready
  SuperSwing:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing")
    CALI B 1
	CALI A 0 A_Recoil(-8)
    CALI C 1 A_CustomPunch(60, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI D 1 A_CustomPunch(100, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI E 1 A_CustomPunch(140, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI F 1 A_CustomPunch(400, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI E 2 A_CustomPunch(120, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI D 2 A_CustomPunch(80, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI C 2 A_CustomPunch(40, CPF_PULLIN, 0, BulletPuff, 250, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    CALI B 1
    Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("Soul") < 1, "NoSurge")
    TNT1 A 0 A_JumpIf(CountInv("Soul") > 0, "SoulSurge")
  SoulSurge:
    TNT1 A 0 A_PlaySound("sonicdoom/soulsurge")
	TNT1 A 0 A_ZoomFactor(0.90, ZOOM_NOSCALETURNING)
	TNT1 A 0 A_TakeInventory("Soul",1)
	TNT1 A 1 A_Recoil(-80)
	TNT1 A 0 A_Light(10)
	CALI G 0 A_FireBullets(28, 12, 400, 20, "InvisiblePuff")
	CALI G 10 A_CustomPunch(99999, CPF_PULLIN, 0, "InvisiblePuff", 150, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_Light(0)
	TNT1 A 0 A_ZoomFactor(1.0)
	Goto Ready
  NoSurge:
    TNT1 A 0 A_Print("Grr... I don't have any Soul left!")
	TNT1 A 0 A_PlaySound("sonicdoom/error")
	Goto Ready
  Spawn:
    CBRN A -1
    Stop
  }
}

ACTOR "Speedforce Railgun" : Weapon replaces Pistol
{
  Weapon.SelectionOrder 100
  Weapon.Kickback 9999999
  Weapon.AmmoType "ChaosEnergy"
  Weapon.AmmoGive 30
  Weapon.SlotNumber 2
  AttackSound "weapons/rbeam"
  Inventory.PickupMessage "Picked up the Speedforce handheld railgun!"
  +EXTREMEDEATH
  +NOAUTOFIRE
  +FORCEPAIN
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.ALT_AMMO_OPTIONAL
  States
  {
  Ready:
    SPFR A 1 A_WeaponReady
    Loop
  Deselect:
    SPFR A 1 A_Lower
    Loop
  Select:
    SPFR A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") < 1, "StandardFire")
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") > 0, "SuperShot")
  StandardFire:
    SPFR A 0 A_JumpIf(CountInv("ChaosEnergy") < 1, "WeakShot")
    SPFR A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "Shoot")
  Shoot:
    SPFR A 0 Bright A_RailAttack(40, 0, 1, "00 b8 ff", "00 2b ff", 0, 0, "none", 0, 0, 0, 25, 1.0, 1.0)
	SPFR A 0 A_TakeInventory("ChaosEnergy", 1)
    SPFR ABCD 1
    Goto Ready
  WeakShot:
    SPFR A 1 A_RailAttack(1, 0, 1, "45 45 45", "45 45 45", 0, 0, "none", 0, 0, 0, 10, 0.5, 0.5)
    SPFR ABCD 1
    Goto Ready
  AltFire:
    SPFR A 0 A_JumpIf(CountInv("ChaosEnergy") < 10, "FailCharge")
    SPFR A 0 A_JumpIf(CountInv("ChaosEnergy") > 9, "AltShot")
  SuperShot:
    SPFR A 0 Bright A_RailAttack(320, 0, 1, "ff ff 00", "ff b4 00", 0, 0, "none", 0, 0, 0, 100, 4.0, 3.0)
    SPFR ABCD 1
    Goto Ready
  AltShot:
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") < 1, "ChargedShot")
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") > 0, "SuperShot")
  ChargedShot:
    SPFR A 1 Bright A_RailAttack(160, 0, 1, "ff 00 00", "ff b4 00", 0, 0, "none", 0, 0, 0, 50, 2.0, 1.5)
	SPFR A 0 A_Recoil(40)
	SPFR A 0 A_TakeInventory("ChaosEnergy", 10)
    SPFR ABCD 1
    Goto Ready
  FailCharge:
    TNT1 A 0 A_Print("I need 10 Chaos energy for a charged burst!")
	TNT1 A 0 A_PlaySound("sonicdoom/error")
	Goto Ready
  Spawn:
    SPER A -1
    Stop
  }
}

ACTOR "Speedy Shotgun" : Weapon replaces Shotgun 1221
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse1 5
  Weapon.AmmoUse2 120
  Weapon.AmmoGive 60
  Weapon.SlotNumber 3
  Weapon.KickBack 160
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 2.0
  Inventory.PickupMessage "Cool, a shotgun!"
  States
  {
  Ready:
    SPSG A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	SPSG A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	SPSG A 1 A_Lower
	Goto Deselect+1
  Fire:
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") < 1, "SpeedyShot")
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") > 0, "WTFShot")
  SpeedyShot:
    SPSG A 0 A_PlaySound("sonicdoom/speedyshot")
	SPSG A 0 A_FireBullets(1, 2, 10, 4)
    SPSG BCDEFG 1
	SPSG GEC 1
	Goto Ready
  WTFShot:
    SPSG A 0 A_PlaySound("sonicdoom/speedyshot")
	SPSG A 0 A_FireBullets(3, 6, 50, 8)
	SPSG A 0 A_GiveInventory("ChaosEnergy", 5)
    SPSG DF 1
	SPSG GE 1
	Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") < 1, "SpeedyBlast")
    TNT1 A 0 A_JumpIf(CountInv("PowerInvulnerable") > 0, "WTFBlast")
  SpeedyBlast:
    SPSG A 0 A_PlaySound("sonicdoom/speedyblast")
	SPSG A 0 A_FireBullets(6, 8, 60, 4)
	SPSG A 7 A_GunFlash
    SPSG BCDEFG 1 
	TNT1 A 8 A_PlaySound("sonicdoom/speedyreload")
	SPSG GFEDCB 1
	SPSG A 9
    Goto Ready
  WTFBlast:
    SPSG A 0 A_PlaySound("sonicdoom/speedyblast")
	SPSG A 0 A_FireBullets(10, 13, 500, 100)
	SPSG A 0 A_GiveInventory("ChaosEnergy", 120)
    SPSG BCDEFG 1
	SPSG GFEDCB 1
	Goto Ready
  Flash:
    SHTF A 4 Bright A_Light1
    SHTF B 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SPEG A -1
    Stop
  }
}

ACTOR "Eclipse Blaster" : Weapon replaces Chaingun 1222
{
  Weapon.SelectionOrder 1400
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse 5
  Weapon.AmmoGive 25
  Weapon.SlotNumber 4
  Weapon.KickBack 100000000
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  Inventory.PickupMessage "Eclipse Blaster! Now we're talking."
  +EXTREMEDEATH
  States
  {
  Ready:
    ECBL A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	ECBL A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	ECBL A 1 A_Lower
	Goto Deselect+1
  Fire:
	ECBL B 1
	{
	  if (CountInv("PowerInvulnerable")) {
        A_FireProjectile("ChaosEnergyBall", 0, 0);
	  }
	  else {
	    A_FireProjectile("ChaosEnergyBall", 0, 1);
	  }
	}
    Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "RingCheck")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") < 1, "NoEnergy")
  RingCheck:
    TNT1 A 0 A_JumpIf(Health > 998, "RingsFull")
    TNT1 A 0 A_JumpIf(Health < 999, "ChaosHeal")
  NoEnergy:
    TNT1 A 0 A_Print("Dang it, no Chaos Energy left!")
	TNT1 A 0 A_PlaySound("sonicdoom/error", 4096)
	Goto Ready
  RingsFull:
    TNT1 A 0 A_Print("I'm already at full Rings!")
	TNT1 A 0 A_PlaySound("sonicdoom/error", 4096)
	Goto Ready
  ChaosHeal:
    ECBL C 0 A_PlaySound("sonicdoom/healthcharge", 4096)
    ECBL C 2 A_TakeInventory("ChaosEnergy", 1)
    ECBL C 0 A_GiveInventory("Ring", 4)
	Goto Ready
  Spawn:
    ECLI A -1
    Stop
  }
}

ACTOR "Rocket Stream" : Weapon replaces RocketLauncher 1223
{
  Weapon.SelectionOrder 1500
  Weapon.AmmoUse1 40
  Weapon.AmmoUse2 400
  Weapon.AmmoGive 80
  Weapon.SlotNumber 5
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Inventory.PickupMessage "Whoa, a Rocket Stream launcher?!"
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  States
  {
  Ready:
    MISG A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	MISG A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	MISG A 1 A_Lower
	Goto Deselect+1
  Fire:
    MISG B 1 A_GunFlash
    MISG B 0
	{
	  if (CountInv("PowerInvulnerable")) {
        A_FireProjectile("ChaosRocket", 0, 0);
	  }
	  else {
	    A_FireProjectile("ChaosRocket", 0, 1);
	  }
	}
    MISG B 0 A_ReFire
    Goto Ready
  Flash:
    MISF A 3 Bright A_Light1
    MISF B 4 Bright
    MISF CD 4 Bright A_Light2
    Goto LightDone
  Spawn:
    LAUN A -1
    Stop
  }
}

ACTOR ChaosRocket
{
  Radius 11
  Height 8
  Speed 30
  Damage 50
  Projectile
  +RANDOMIZE
  +DEHEXPLOSION
  +ROCKETTRAIL
  SeeSound "weapons/rocklf"
  DeathSound "weapons/rocklx"
  States
  {
  Spawn:
    MISL A 1 Bright
    Loop
  Death:
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

ACTOR ChaosEnergyBall
{
  Radius 13
  Height 8
  Speed 60
  Damage 20
  Projectile
  +RANDOMIZE
  +ZDOOMTRANS
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/plasmaf"
  DeathSound "weapons/plasmax"
  States
  {
  Spawn:
    PLSS AB 6 Bright
    Loop
  Death:
    PLSE ABCDE 4 Bright
    Stop
  }
}

ACTOR InvisiblePuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  +EXTREMEDEATH
  RenderStyle None
  Alpha 0.5
  VSpeed 1
  Mass 5
  States
  {
  Spawn:
    PUFF A 4 Bright
    PUFF B 4
  Melee:
    PUFF CD 4
    Stop
  }
}

ACTOR BoostCheck : Inventory 
{
  Inventory.MaxAmount 1
}