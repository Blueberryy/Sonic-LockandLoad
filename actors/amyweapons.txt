ACTOR AmyAbilities : Weapon 1210
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 9999999999999
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 0.0
  Tag "Amy Rose"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    TNT1 A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	Goto Select
  Select:
	TNT1 A 1 A_Lower
	Goto Deselect
  Fire:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing", 1)
    AKIK ABC 1
    AKIK D 1
	{
	  if (CountInv("PowerSuper") < 1) 
	  {
	    A_CustomPunch(40, false, CPF_NOTURN, "NoGibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
	  }
	  else if (CountInv("PowerSuper") > 0) 
	  {
	    A_CustomPunch(400, false, CPF_NOTURN, "NoGibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit");
	  }
	}
    AKIK EFG 1
	TNT1 A 2
    Goto Ready
  AltFire:
    // Amy's sliding kick
    TNT1 A 0 A_PlaySound("sonicdoom/sliding", 7, 1.0, true)
	TNT1 A 0 A_SetGravity(1.5)
	TNT1 A 0 A_Recoil(-20)
	SLID ABCDE 1 A_CustomPunch(4 * random(1, 10), true, CPF_NOTURN, "NoGibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	SLID EDCBA 1
	TNT1 A 0 A_StopSound(7)
	TNT1 A 0 A_SetGravity(1.0)
	Goto Ready
  AltHold:
	TNT1 A 0 A_Recoil(-20)
    SLID E 1 A_CustomPunch(4 * random(1, 10), true, CPF_NOTURN, "NoGibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	SLID EDCBA 1
	TNT1 A 0 A_StopSound(7)
	TNT1 A 0 A_SetGravity(1.0)
	Goto Ready
  Spawn:
    AMYH A -1
    Stop
  }
}

ACTOR PikoPikoHammer : Weapon 
{
  Weapon.SelectionOrder 1
  Weapon.Kickback 10
  Weapon.AmmoType "GlideTime"
  Inventory.PickupMessage "\c[AmyPink]My Piko Piko hammer!"
  Tag "Piko Piko Hammer"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOALERT
  States
  {
  Ready:
    PIKO A 1 A_WeaponReady
    Loop
  Deselect:
    PIKO A 1 A_Lower
    Loop
  Select:
    PIKO A 1 A_Raise
    Loop
  Fire:
    PIKO A 0 A_PlaySound("sonicdoom/hammerswing",1)
	PIKO A 0 ACS_Execute(2)
    PIKO BCD 1
    PIKO E 4 A_CustomPunch(200, CPF_PULLIN, 0, "GibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	PIKO A 0 A_ReFire
    PIKO DCB 1
	PIKO A 0 A_SetGravity(1)
    Goto Ready
  Hold:
    TNT1 A 0
	{
	  if (CountInv("GuardCheck") < 1)
	  {
		A_GiveInventory("GuardCheck");
		if (CountInv("PowerBlocking") < 1)
		{
		  A_PlaySound("sonicdoom/blockready", 6);
		}
	  }
	}
    PIKO E 1
	PIKO A 0 A_SetGravity(1)
	PIKO A 0 A_ReFire
	PIKO A 0 
	{
	  if (CountInv("ChaosEnergy") > 19)
	  {
	    A_GiveInventory("HammerBlock");
      }
	}
    PIKO DCB 1
	PIKO A 0 A_TakeInventory("GuardCheck")
    Goto Ready
  AltFire:
    PIKO F 1
	{
	  if (CountInv("GlideTime") > 0) 
	  {
	    A_NoGravity;
	    A_SetInventory("GlideStage", 1);
		A_PlaySound("sonicdoom/glidestart", 1, 1.0, true);
	  }
	  else
	  {
	    A_Gravity;
	    if (CountInv("PowerSuper") < 1) 
	    {
	      A_SetInventory("GlideStage", 2);
	    }
	    else
	    {
	      A_SetInventory("GlideStage", 0);
	    }
	  }
	}
	PIKO A 0 
	{
	  if (CountInv("PowerSuper") < 1) 
	  {
	    A_TakeInventory("GlideTime", 1);
	  }
	}
	PIKO A 0 A_ReFire
	PIKO A 0 
	{
	  if (CountInv("PowerSuper") < 1) 
	  {
	    A_SetInventory("GlideStage", 2);
	  }
	  else
	  {
	    A_SetInventory("GlideStage", 0);
	  }
	}
	PIKO A 0 A_SetGravity(1)
	PIKO A 0 A_StopSound(1)
	PIKO A 0 A_PlaySound("sonicdoom/glideend",1)
	PIKO AAAAAAAAAAAAAAAAAAAA 1 A_GiveInventory("GlideTime", 5)
	PIKO A 0 A_SetInventory("GlideStage", 0)
	Goto Ready
  Spawn:
    PKPK A -1
    Stop
  }
}

ACTOR SilentRosePistol : Weapon
{
  Weapon.SelectionOrder 100
  Weapon.Kickback 0
  Weapon.AmmoType "ChaosEnergy"
  Weapon.AmmoUse 2
  Weapon.AmmoGive 90
  AttackSound "sonicdoom/rosepistol"
  Inventory.PickupMessage "\c[AmyPink]The Silent Rose, both pretty AND deadly!"
  Inventory.PickupSound "sonicdoom/amypickup"
  Tag "Silent Rose"
  +WEAPON.NOALERT
  States
  {
  Ready:
    RSFP A 1 A_WeaponReady
    Loop
  Deselect:
    RSFP A 1 A_Lower
    Loop
  Select:
    RSFP A 1 A_Raise
    Loop
  Fire:
    RSFP A 0 A_JumpIf(CountInv("PowerSuper") > 0, "SuperShot")
	RSFP A 0 A_FireBullets(0, 0, 1, 50, "NoGibPuff", FBF_USEAMMO|FBF_NORANDOM)
    RSFP ABCBA 1
    RSFP DEFGHIHGFED 1
    Goto Ready
  SuperShot:
    RSFP A 0 A_FireBullets(0, 0, 1, 50, "NoGibPuff", 0)
    RSFP ABCBA 1
	RSFP A 0 A_ReFire
    RSFP DEFGHIHGFED 1
    Goto Ready
  Spawn:
    ROSP A -1
    Stop
  }
}

ACTOR PrecisionShotgun : Weapon
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoType "ChaosEnergy"
  Weapon.AmmoUse1 5
  Weapon.AmmoGive 60
  Weapon.KickBack 160
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 2.0
  Inventory.PickupMessage "\c[AmyPink]The Precision Shotgun! My favorite weapon!!"
  Tag "Precision Shotgun"
  +ALT_AMMO_OPTIONAL
  +NOAUTOFIRE
  +NOALERT
  States
  {
  Ready:
    SPSG A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	SPSG A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	SPSG A 1 A_Lower
	Goto Deselect+1
  Fire:
	SPSG A 0 A_PlaySound("sonicdoom/speedyshot")
	SPSG A 0 A_GunFlash
    SPSG A 0 
	{
	  if (CountInv("PowerSuper") > 0) {
	    A_FireBullets(4, 1, 10, 8, "BulletPuff", 0);
	  }
	  else
	  {
	    A_FireBullets(4, 1, 10, 8, "BulletPuff", FBF_USEAMMO);
	  }
	}
    SPSG A 0 A_AlertMonsters(0)
	SPSG A 5
    SPSG BCDEFG 1 
	TNT1 A 8 A_PlaySound("sonicdoom/speedyreload")
	SPSG GFEDCB 1
	SPSG A 5 
	SPSG A 0 A_ReFire
	Goto Ready
  PreciseShot:
	SPSG A 0 
	{
	  if (CountInv("ZoomCharge") > 99)
	  {
	    if (CountInv("PowerSuper") < 1)
	    {
	      A_TakeInventory("ChaosEnergy", 120);
	    }
	    A_FireBullets(0, 0, 50, 16, "NoGibPuff");
		A_PlaySound("sonicdoom/zoomfire", 6);
	  }
	  A_TakeInventory("ZoomCharge", 100);
	}
	Goto Ready
  NoShot:
    SPSG A 0 A_Print("\c[AmyPink]I don't have enough energy!")
	SPSG A 0 A_PlaySound("sonicdoom/amyerror")
	Goto Ready
  AltFire:
    SPSG A 0 A_JumpIf(CountInv("ChaosEnergy") < 120, "NoShot")
    AIMA A 0 A_ZoomFactor(4)
	AIMA A 0
	{
	  if (CountInv("ZoomCheck2") < 1) 
	  {
	    A_PlaySound("sonicdoom/zoom", 6);
	    A_GiveInventory("ZoomCheck2");
	  }
	}
    AIMA A 0 A_GiveInventory("ZoomCheck")
	AIMA A 0 
	{
	  if (CountInv("PowerSuper") > 0)
	  {
	    A_GiveInventory("ZoomCharge", 100);
	  }
	  else
	  {
	    A_GiveInventory("ZoomCharge", 2);
	  }
	}
    AIMA A 0 A_SetSpeed(0.5)
	AIMA A 1 A_WeaponReady(WRF_NOBOB|WRF_DISABLESWITCH|WRF_NOPRIMARY)
	AIMA A 0 A_ReFire
	AIMA A 0 A_PlaySound("sonicdoom/zoomend", 6)
	AIMA A 0 A_ZoomFactor(1)
    AIMA A 0 A_SetSpeed(1.5)
	AIMA A 0 A_TakeInventory("ZoomCheck")
	AIMA A 0 A_TakeInventory("ZoomCheck2")
    Goto PreciseShot
  Flash:
    SHTF A 2 Bright A_Light1
    SHTF B 1 Bright A_Light2
    Goto LightDone
  Spawn:
    SPEG A -1
    Stop
  }
}

ACTOR AmyEclipseBlaster : Weapon
{
  Weapon.SelectionOrder 1400
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse 5
  Weapon.AmmoGive 50
  Weapon.KickBack 100000000
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  +EXTREMEDEATH
  +NOALERT
  Inventory.PickupMessage "\c[AmyPink]The Eclipse Blaster Mk. IV! Sonic didn't want this version."
  Tag "Eclipse Blaster Mk. IV"
  States
  {
  Ready:
    ACBL A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	ACBL A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	ACBL A 1 A_Lower
	Goto Deselect+1
  Fire:
	ACBL B 4
	{
	  A_AlertMonsters;
	  if (CountInv("PowerSuper")) {
        A_FireProjectile("ChaosEnergyBallAmy", 0, 0);
	  }
	  else {
	    A_FireProjectile("ChaosEnergyBallAmy", 0, 1);
	  }
	}
    Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "RingCheck")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") < 1, "NoEnergy")
  RingCheck:
    TNT1 A 0 A_JumpIf(Health > 998, "RingsFull")
    TNT1 A 0 A_JumpIf(Health < 999, "ChaosHeal")
  NoEnergy:
    TNT1 A 0 A_Print("\c[AmyPink]I'm out of energy!")
	TNT1 A 0 A_PlaySound("sonicdoom/amyerror", 4096)
	Goto Ready
  RingsFull:
    TNT1 A 0 A_Print("\c[AmyPink]I'm already at full Rings!")
	TNT1 A 0 A_PlaySound("sonicdoom/amyerror", 4096)
	Goto Ready
  ChaosHeal:
    ECBL C 0 A_PlaySound("sonicdoom/healthcharge", 4096)
    ECBL C 2 A_TakeInventory("ChaosEnergy", 1)
    ECBL C 0 A_GiveInventory("Ring", 4)
	Goto Ready
  Spawn:
    ACLI A -1
    Stop

  }
}

ACTOR RapidMissiles : Weapon
{
  Weapon.SelectionOrder 1500
  Weapon.AmmoUse1 10
  Weapon.AmmoGive 80
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Inventory.PickupMessage "\c[AmyPink]Wow! A rapid missile launcher!"
  Tag "Rapid Missiles"
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  States
  {
  Ready:
    MMSL A 1 A_WeaponReady
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	MMSL A 1 A_Lower
	Goto Select+1
  Select:
    TNT1 AA 0 A_Lower
	MMSL A 1 A_Lower
	Goto Deselect+1
  Fire:
    MMSL A 2
	{
	  if (CountInv("PowerSuper")) {
        A_FireProjectile("ChaosMissile", 0, 0, -3, 0);
	  }
	  else {
	    A_FireProjectile("ChaosMissile", 0, 1, -3, 0);
	  }
	}
	MMSL B 2
	MMSL A 2
	{
	  if (CountInv("PowerSuper")) {
        A_FireProjectile("ChaosMissile", 0, 0, 0, 0);
	  }
	  else {
	    A_FireProjectile("ChaosMissile", 0, 1, 0, 0);
	  }
	}
	MMSL B 2
	MMSL A 2
	{
	  if (CountInv("PowerSuper")) {
        A_FireProjectile("ChaosMissile", 0, 0, 3, 0);
	  }
	  else {
	    A_FireProjectile("ChaosMissile", 0, 1, 3, 0);
	  }
	}
	MMSL A 4
    MMSL B 0 A_ReFire
    Goto Ready
  Spawn:
    MICR A -1
    Stop
  }
}