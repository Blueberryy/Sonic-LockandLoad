ACTOR SonicAbilities : Weapon
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 9999999999999
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 0.0
  Weapon.AmmoType "ChaosEnergy"
  Tag "Sonic the Hedgehog"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	Goto Select
  Select:
	TNT1 A 1 A_Lower
	Goto Deselect
  Fire:
	TNT1 A 0 A_JumpIf(CountInv("SpinCharges")>20,"LightSpeedAttack")
	TNT1 A 2
	{
	  A_OverlayFlags(9, PSPF_ADDWEAPON, true);
	  A_Overlay(9, "SpinAnim", true);
	  A_GiveInventory("PeeloutCheck");
	  A_PlaySound("sonicdoom/peelout", 5);
	  A_GiveInventory("SpinCharges", 1);
	  A_Light(3);
	}
	TNT1 A 0 A_SetSpeed(0.1)
	TNT1 A 1 A_ReFire
	TNT1 A 0 A_ClearOverlays(0, 0)
	TNT1 A 0 A_JumpIf(CountInv("SpinCharges")>8,"SpinDash")
  Spin:
	TNT1 A 0 A_SetSpeed(4)
    TNT1 A 0 A_PlaySound("sonicdoom/peeloutrelease", 5)
	TNT1 A 0 A_TakeInventory("PeeloutCheck")
	TNT1 A 0 A_TakeInventory("SpinCharges", 99)
	TNT1 A 0 A_Light(0)
	TNT1 AAAA 1
	{
		A_ChangeVelocity (Cos(Pitch) * 8, 0, Sin(Pitch) * -8, CVF_Relative);
		A_CustomPunch(25, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/hit");
	}
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  SpinDash:
	TNT1 A 0 A_SetSpeed(4)
    TNT1 A 0 A_PlaySound("sonicdoom/peeloutrelease", 5)
	TNT1 A 0 A_TakeInventory("PeeloutCheck")
	TNT1 A 0 A_TakeInventory("SpinCharges", 99)
	TNT1 A 0 A_Light(0)
	TNT1 AAAAAA 1
	{
		A_ChangeVelocity (Cos(Pitch) * 16, 0, Sin(Pitch) * -16, CVF_Relative);
		A_CustomPunch(50, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/hit");
	}
	Goto Ready
  LightSpeedAttack:
	TNT1 A 0 A_ClearOverlays(0, 0)
	TNT1 A 0 A_StopSound(5)
	TNT1 A 0 A_SetSpeed(1.5)
	TNT1 A 0 A_TakeInventory("PeeloutCheck")
    TNT1 A 1
	{
	  if (CountInv("LightSpeedCheck") < 1)
	  {
		A_GiveInventory("LightSpeedCheck");
	    A_PlaySound("sonicdoom/herewe", 6);
	  }
	}
	TNT1 A 0 A_ReFire
	TNT1 A 0 A_TakeInventory("SpinCharges", 99)
	TNT1 A 0 A_TakeInventory("LightSpeedCheck")
	TNT1 A 0 A_SetSpeed(8)
	TNT1 A 0 A_PlaySound("sonicdoom/go", 6)
    TNT1 A 0 A_PlaySound("sonicdoom/peeloutrelease", 7)
	TNT1 A 0 A_Light(0)
	TNT1 A 0 A_FireBullets(0, 0, 10, 24, "LightAttack")
	TNT1 A 1 A_ChangeVelocity (Cos(Pitch) * 256, 0, Sin(Pitch) * -256, CVF_Relative)
	Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("PowerSuper") > 0, "SuperBoost")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "Boost")
	TNT1 A 0 A_Recoil(-20)
    TNT1 A 0 A_PlaySound("sonicdoom/dash", 6)
	TNT1 A 0 A_Gravity
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  Boost:
    TNT1 A 0
	{
	  if (CountInv("BoostCheck") < 1)
	  {
		A_GiveInventory("BoostCheck");
	    A_PlaySound("sonicdoom/boost", 6);
	  }
	}
    TNT1 A 0 A_SetMugShotState("Boost")
    TNT1 A 0 A_PlaySound("sonicdoom/boosttrail", 1, 1.0, true)
	TNT1 A 0
	{
		if (GetCvar("VerticalBoost"))
		{
			A_ChangeVelocity (Cos(Pitch) * 20, 0, Sin(Pitch) * -20, CVF_Relative);
		}
		else
		{
			A_LowGravity();
			A_Recoil(-40);
		}
	}
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_TakeInventory("ChaosEnergy", 1)
    BOOS A 1 A_CustomPunch(5, false, CPF_NOTURN, "GibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	TNT1 A 0
	{
		if (!GetCvar("VerticalBoost"))
		{
			A_Gravity();
		}
	}
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  SuperBoost:
    TNT1 A 0
	{
	  if (CountInv("BoostCheck") < 1)
	  {
		A_GiveInventory("BoostCheck");
	    A_PlaySound("sonicdoom/boost", 6);
	  }
	}
    TNT1 A 0 A_SetMugShotState("SuperBoost")
    TNT1 A 0 A_PlaySound("sonicdoom/boosttrail", 1, 1.0, true)
	TNT1 A 0
	{
		if (GetCvar("VerticalBoost"))
		{
			A_ChangeVelocity (Cos(Pitch) * 30, 0, Sin(Pitch) * -20, CVF_Relative);
		}
		else
		{
			A_LowGravity();
			A_Recoil(-60);
		}
	}
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_TakeInventory("ChaosEnergy", 1)
    BOOS B 1 A_CustomPunch(999, false, CPF_NOTURN, "GibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	TNT1 A 0
	{
		if (!GetCvar("VerticalBoost"))
		{
			A_Gravity();
		}
	}
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
	Goto Ready
  SpinAnim:
	SPIN ABCDEFEDCB 1
    Loop
  SpinAnimEnd:
    Goto Ready
  Spawn:
    SONC A -1
    Stop
  }
} 
ACTOR Caliburn : Weapon 
{
  Weapon.SelectionOrder 1
  Weapon.Kickback 1
  Inventory.PickupMessage "\c[SonicBlue]I like sword -- that's a personal weapon!\n\c[CaliburnGold]*sigh*"
  Inventory.PickupSound "sonicdoom/sonicpickup"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    CALI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	TNT1 A 0 A_Gravity
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    CALI A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    CALI A 1 A_Raise
    Goto Select+1
  Fire:
	TNT1 A 0 A_CheckFloor("GroundAttack")
	Goto AirStart
  GroundAttack:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    TNT1 A 0 A_PlaySound("sonicdoom/grunt",7)
	TNT1 A 0
	{
	  if (CountInv("PowerSuper") > 0)
	  {
		A_FireProjectile("EnergyBladeLeft");
	  }
	}
    CALI BCDE 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	CALI E 5
    CALI A 0 A_ReFire
	Goto Ready
  AirStart:
	TNT1 A 0 A_PlaySound("sonicdoom/airgrunt",7)
	CSPN A 0 ThrustThingZ(0, 30, 0, 1)
  AirAttack:
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    CSPN A 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN B 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN C 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN D 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN E 1
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN F 1
	TNT1 A 0 ThrustThingZ(0, 5, 1, 1)
    CSPN G 1
    CALI B 0 A_CheckFloor("Ready")
    Goto AirAttack
  Hold:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
	TNT1 A 0
	{
	  if (CountInv("PowerSuper") > 0)
	  {
		A_FireProjectile("EnergyBladeRight");
	  }
	}
	
    CALI FGHI 1 A_CustomPunch(45, CPF_PULLIN, 0, "InvisiblePuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	CALI J 5
    CALI A 0 A_ReFire("Fire")
	Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("PowerSuper") > 0, "SuperBoost")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "Boost")
	TNT1 A 0 A_Print("\c[SonicBlue]I'm all out of energy!")
    TNT1 A 0 A_PlaySound("sonicdoom/error", 4096)
	TNT1 A 0 A_Gravity
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  Boost:
    TNT1 A 0
	{
	  if (CountInv("BoostCheck") < 1)
	  {
		A_GiveInventory("BoostCheck");
	    A_PlaySound("sonicdoom/boost", 6);
	  }
	}
    TNT1 A 0 A_SetMugShotState("Boost")
    TNT1 A 0 A_PlaySound("sonicdoom/boosttrail", 1, 1.0, true)
	TNT1 A 0
	{
		if (GetCvar("VerticalBoost"))
		{
			A_ChangeVelocity (Cos(Pitch) * 20, 0, Sin(Pitch) * -20, CVF_Relative);
		}
		else
		{
			A_LowGravity();
			A_Recoil(-40);
		}
	}
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_TakeInventory("ChaosEnergy", 1)
    CBOO A 1 A_CustomPunch(15, false, CPF_NOTURN, "GibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	TNT1 A 0
	{
		if (!GetCvar("VerticalBoost"))
		{
			A_Gravity();
		}
	}
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  SuperBoost:
    TNT1 A 0
	{
	  if (CountInv("BoostCheck") < 1)
	  {
		A_GiveInventory("BoostCheck");
	    A_PlaySound("sonicdoom/boost", 6);
	  }
	}
    TNT1 A 0 A_SetMugShotState("Super Boost")
    TNT1 A 0 A_PlaySound("sonicdoom/boosttrail", 1, 1.0, true)
	TNT1 A 0
	{
		if (GetCvar("VerticalBoost"))
		{
			A_ChangeVelocity (Cos(Pitch) * 20, 0, Sin(Pitch) * -20, CVF_Relative);
		}
		else
		{
			A_LowGravity();
			A_Recoil(-40);
		}
	}
	TNT1 A 0 A_Light(2)
	TNT1 A 0 A_TakeInventory("ChaosEnergy", 1)
    CBOO B 1 A_CustomPunch(999, false, CPF_NOTURN, "GibPuff", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
	TNT1 A 0 A_ReFire
	TNT1 A 0
	{
		if (!GetCvar("VerticalBoost"))
		{
			A_Gravity();
		}
	}
	TNT1 A 0 A_TakeInventory("BoostCheck")
	TNT1 A 0 A_StopSound(1)
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Spawn:
    CBRN A -1
    Stop
  }
}

ACTOR AcceleratedPistol : Weapon
{
  Weapon.SelectionOrder 100
  Weapon.Kickback 1
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse1 1
  Weapon.AmmoUse2 4
  Weapon.AmmoGive 60
  Inventory.PickupMessage "Picked up a pistol."
  Tag "Accelerated Pistol"
  +FORCEPAIN
  +PUFFONACTORS
  +ALWAYSPUFF
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    SPIS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    SPIS A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    SPIS A 1 A_Raise
    Goto Select+1
  Fire:
    TNT1 A 0
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 1);
		}
	}
    SPIS A 0 A_PlaySound("sonicdoom/sonicpistol", 1)
    SPIS A 0
	{
		if (CountInv("PowerSuper"))
		{
			A_FireBullets(0, 0, 1, 16, "NoGibPuff");
		}
		else
		{
			A_FireBullets(2, 2, 1, 12, "NoGibPuff");
		}
	}
	TNT1 A 0 A_TakeInventory("KickCooldown", 4)
	SPIS A 1 A_GunFlash
	SPIS BCA 1
	SPIS A 0 A_ReFire
    Goto Ready
  AltFire:
	TNT1 A 0 A_TakeInventory("KickCooldown", 4)
    SPIS A 0
	{
		A_PlaySound("weapons/rbeam");
		if (CountInv("PowerSuper") > 0)
		{
			A_RailAttack(96, 0, 1, "06 b4 00", "fd ff 00", RGF_FULLBRIGHT, 0, "GibPuff", 0, 0, 0, 25, 1.0, 1.0);
			A_ChangeVelocity (Cos(Pitch) * -50, 0, Sin(Pitch) * 50, CVF_Relative);
			A_GiveInventory("ChaosEnergy", 4);
		}
		else
		{
			A_RailAttack(24, 0, 1, "c5 fe ff", "c5 c6 ff", RGF_FULLBRIGHT, 0, "NoGibPuff", 0, 0, 0, 25, 1.0, 1.0);
			A_ChangeVelocity (Cos(Pitch) * -25, 0, Sin(Pitch) * 25, CVF_Relative);
		}
	}
    SPIS BC 1
	SPIS C 2
    Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Flash:
    SPIF A 1 Bright A_Light1
    Goto LightDone
  Spawn:
    PIST A -1
    Stop
  }
}


ACTOR RapidShotty : Weapon 1221
{
  Weapon.SelectionOrder 1300
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse1 16
  Weapon.AmmoUse2 42
  Weapon.AmmoGive 64
  Weapon.Kickback 1000
  Inventory.PickupMessage "\c[SonicBlue]I'm runnin' circles around ya!"
  Tag "Rapid Shotty"
  +PUFFONACTORS
  +ALWAYSPUFF
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    SCTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    SCTG A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    SCTG A 1 A_Raise
    Goto Select+1
  Fire:
	TNT1 A 0 A_TakeInventory("KickCooldown", 110)
    TNT1 A 0 A_JumpIf(CountInv("PowerSuper") > 0, "SuperFire")
    SCTG A 0 A_PlaySound("sonicdoom/shardshot", 1)
	SCTG A 0 A_FireBullets(18, 10, 20, 4, "BulletPuff")
	SCTG B 6 A_GunFlash
	SCTG C 4
	SCTG C 6
	SCTG A 0 A_ReFire
	Goto Ready
  AltFire:
	TNT1 A 0 A_TakeInventory("KickCooldown", 60)
    SCTG A 0 A_PlaySound("sonicdoom/forceshot", 1)
	SCTG A 0 A_ChangeVelocity(Cos(Pitch) * -20, 0, Sin(Pitch) * 20, CVF_Relative)
	SCTG A 0 A_FireBullets(8, 2, 24, 4, "BulletPuff")
	SCTG B 12 A_GunFlash("ForceFlash")
	SCTG A 0 { if (CountInv("PowerSuper") > 0) { A_GiveInventory("ChaosEnergy", 42); A_ReFire; } }
	SCTG CDE 1
	SCTG F 28 A_PlaySound("sonicdoom/forcereload", 6)
	SCTG EDC 1
	SCTG A 14
	SCTG A 0 A_ReFire
	Goto Ready
  SuperFire:
	TNT1 A 0 A_TakeInventory("KickCooldown", 8)
    TNT1 A 0 A_GiveInventory("ChaosEnergy", 16)
    SCTG A 0 A_PlaySound("sonicdoom/shardshot", 1)
	SCTG A 0 A_FireBullets(18, 10, 20, 4, "BulletPuff")
	SCTG B 3 A_GunFlash("SuperFlash")
	SCTG C 2
	SCTG C 3
	SCTG A 0 A_ReFire
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Flash:
    SCTF A 4 Bright
	Stop
  SuperFlash:
    SCTF A 2 Bright
	Stop
  ForceFlash:
    SCTF B 4 Bright
	Stop
  Spawn:
    SCAT A -1
    Stop
  }
}

ACTOR ShurikenCrossbow : Weapon 
{
  Weapon.SelectionOrder 800
  Weapon.AmmoUse 24
  Weapon.AmmoGive 100
  Weapon.Kickback 1
  Weapon.AmmoType "ChaosEnergy"
  Inventory.PickupMessage "\c[SonicBlue]Is that a crossbow that fires shurikens?! Awesome!!"
  Tag "Shuriken Crossbow"
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    SHRL A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    SHRL A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    SHRL A 1 A_Raise
    Goto Select+1
  Fire:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    TNT1 A 0
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 24);
		}
	}
    TNT1 A 0 A_JumpIf(CountInv("PowerSuper") > 0, "SuperFire")
    SHRL B 12 A_FireProjectile("Shuriken", 0, 1)
    SHRL CDEFG 1
	TNT1 A 0 A_ReFire
	Goto Ready
  SuperFire:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    SHRL B 6 A_FireProjectile("Shuriken", 0, 1)
	TNT1 A 1 A_ReFire
    SHRL CDEFG 1
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Spawn:
    SURI A -1
    Stop
  }
}


ACTOR EclipseBlaster : Weapon
{
  Weapon.SelectionOrder 1400
  Weapon.AmmoType1 "ChaosEnergy"
  Weapon.AmmoType2 "ChaosEnergy"
  Weapon.AmmoUse 4
  Weapon.AmmoGive 25
  Weapon.KickBack 100000000
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  +EXTREMEDEATH
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "\c[SonicBlue]The Eclipse Blaster Mk. V, the ultimate in plasma weaponry!"
  Tag "Eclipse Blaster Mk. V"
  States
  {
  Ready:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    ECBL A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    ECBL A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    ECBL A 1 A_Raise
    Goto Select+1
  Fire:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
	ECBL B 1
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_FireProjectile("ChaosEnergyBallEX", 0, 0);
		}
		else
		{
			A_FireProjectile("ChaosEnergyBall", 0, 1);
		}
	}
	TNT1 A 0 A_ReFire
    Goto Ready
  AltFire:
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "RingCheck")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") < 1, "NoEnergy")
  RingCheck:
    TNT1 A 0 A_JumpIf(Health > 299, "RingsFull")
    TNT1 A 0 A_JumpIf(Health < 300, "ChaosHeal")
  NoEnergy:
	TNT1 A 0 A_PlaySound("sonicdoom/error", 4096)
	Goto Ready
  RingsFull:
	TNT1 A 0 A_PlaySound("sonicdoom/error", 4096)
	Goto Ready
  ChaosHeal:
    ECBL C 0 A_PlaySound("sonicdoom/healthcharge", 4096)
    ECBL C 0 A_TakeInventory("ChaosEnergy", 8)
    ECBL C 1 A_GiveInventory("Health", 4)
	TNT1 A 0 A_ReFire
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Spawn:
    ECLI A -1
    Stop

  }
}

ACTOR AirStrikeLauncher : Weapon 1223
{
  Weapon.SelectionOrder 1500
  Weapon.AmmoUse 40
  Weapon.AmmoGive 200
  Weapon.AmmoType "ChaosEnergy"
  Inventory.PickupMessage "\c[SonicBlue]Time for some sick air strikes!"
  Tag "Rocket Rioter"
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 1.0
  Weapon.BobSpeed 1.0
  Weapon.Kickback 1000
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOAUTOAIM
  +WEAPON.ALT_AMMO_OPTIONAL
  States
  {
  Ready:
    MISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	TNT1 A 0 ACS_NamedExecute("KickCooldown")
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
    MISG A 1 A_Lower
    Goto Deselect+1
  Select:
    TNT1 AA 0 A_Raise
    MISG A 1 A_Raise
    Goto Select+1
  Fire:
	TNT1 A 0 A_TakeInventory("KickCooldown", 3)
    TNT1 A 0 A_CheckFloor("StandardRocket")
	TNT1 A 0 A_Jump(256, "AirstrikeRocket")
    Goto Ready
  StandardRocket:
	MISG A 0 A_FireProjectile("ChaosRocket", 0, 1)
    TNT1 A 0
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 40);
		}
	}
	MISG A 1
	MISG B 2
    Goto Ready
  AirstrikeRocket:
	MISG A 0 A_FireProjectile("AirstrikeRocket", 0, 1)
    TNT1 A 0
	{
		if (CountInv("PowerSuper") > 0)
		{
			A_GiveInventory("ChaosEnergy", 40);
		}
	}
	MISG A 0 A_ChangeVelocity (Cos(Pitch) * -10, 0, Sin(Pitch) * 10, CVF_Relative)
	MISG A 1
	MISG B 2
	Goto Ready
  AltFire:
	MISG A 1 A_GiveInventory("RemoteDet")
	MISG B 1 A_ReFire
	MISG A 1 A_TakeInventory("RemoteDet")
  Reload:
	TNT1 A 0 A_GiveInventory("SonicKick")
	Goto Ready
  Spawn:
    LAUN A -1
    Stop
  }
}