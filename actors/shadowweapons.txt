ACTOR ShadowAbilities : Weapon
{
  Weapon.SelectionOrder 3700
  Weapon.Kickback 9999999999999
  Weapon.BobRangeX 0.0
  Weapon.BobRangeY 0.0
  Weapon.AmmoType "ChaosEnergy"
  Tag "Shadow the Hedgehog"
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  States
  {
  Ready:
    TNT1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	TNT1 A 0 A_SetGravity(2.5)
	TNT1 A 0 A_TakeInventory("TeleportCooldown", 1)
	TNT1 A 0
	{
	  if (CountInv("PowerSuper")>0)
	  {
	    A_GiveInventory("ChaosEnergy", 15);
	  }
	}
    Loop
  Deselect:
    TNT1 AA 0 A_Lower
	Goto Select
  Select:
	TNT1 A 1 A_Lower
	Goto Deselect
  Fire:
    TNT1 A 0 A_CheckFloor("Punch")
  Kick:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    DKIK ABC 1
    DKIK D 1 A_CustomPunch(random(1,64)*10, true, CPF_NOTURN, "ShadowMeleeAtk", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    DKIK EF 1
	TNT1 A 2 A_ReFire("Kick2")
    Goto Ready
  Punch:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    DPNR ABC 1
    DPNR D 2 A_CustomPunch(random(1,64)*15, true, CPF_NOTURN, "ShadowMeleeAtk", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    DPNR CBA 1
	TNT1 A 2 A_ReFire("Punch2")
    Goto Ready
  Kick2:
    TNT1 A 0 A_CheckFloor("Punch")
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    DKIK ABC 1
    DKIK D 1 A_CustomPunch(random(1,64)*10, true, CPF_NOTURN, "ShadowMeleeAtk", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    DKIK EF 1
	TNT1 A 2 A_ReFire
  Punch2:
    TNT1 A 0 A_PlaySound("sonicdoom/swordswing",1)
    DPNL ABC 1
    DPNL D 2 A_CustomPunch(random(1,64)*25, true, CPF_NOTURN, "ShadowMeleeAtk", 75, 0, 0, "ArmorBonus", "sonicdoom/finalhit")
    DPNL CBA 1
	TNT1 A 2 A_ReFire
    Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("ShadowDash")
	Goto Ready
  Spawn:
    SHAD A -1
    Stop
  }
}

ACTOR ChaosEmerald : Weapon
{
  Weapon.SelectionOrder 3700
  Weapon.AmmoType "ChaosEnergy"
  Weapon.AmmoUse 50
  Weapon.AmmoGive 100
  Inventory.PickupSound "sonicdoom/damn4thchaos"
  Inventory.PickupMessage "\c[ShadowRedBlack]The damn 4th Chaos Emerald!"
  Tag "Chaos Emerald"
  +AMMO_OPTIONAL
  States
  {
  Ready:
    CARM A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	TNT1 A 0
	{
	  if (CountInv("PowerSuper")>0)
	  {
	    A_GiveInventory("ChaosEnergy", 15);
	  }
	  else
	  {
	    A_GiveInventory("ChaosEnergy", 1);
	  }
	}
    Loop
  Deselect:
    CARM AA 0 A_Lower
	Goto Select
  Select:
	CARM A 1 A_Lower
	Goto Deselect
  Fire:
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") < 25, "NotEnoughEnergy")
	TNT1 A 0 A_PlaySound("sonicdoom/chaosgrunt", 2)
    CARM A 1 A_FireProjectile("SuperChaosEnergyBall")
	CARM BCDE 1
	CARM E 10
	CARM DCB 1
	TNT1 A 0 A_ReFire
    Goto Ready
  AltFire:
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") > 0, "RingCheck")
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergy") < 1, "NotEnoughEnergy")
    Goto Ready
  RingCheck:
    TNT1 A 0 A_JumpIf(CountInv("Health") > 998, "RingsFull")
    TNT1 A 0 A_JumpIf(CountInv("Health") < 999, "ChaosCharge")
    Goto Ready
  NotEnoughEnergy:
    TNT1 A 0 A_JumpIf(CountInv("ChaosEnergyEX") < 1, "NoEnergy")
    TNT1 A 0 A_Print("\c[ShadowRedBlack]I don't have enough energy!")
	TNT1 A 0 A_PlaySound("sonicdoom/shadowerror", 4096)
	Goto Ready
  RingsFull:
    TNT1 A 0 A_Print("\c[ShadowRedBlack]My Rings are full!")
	TNT1 A 0 A_PlaySound("sonicdoom/shadowerror", 4096)
	Goto Ready
  ChaosCharge:
    TNT1 A 0 A_PlaySound("sonicdoom/healthcharge", 4096)
    CARM F 1 A_TakeInventory("ChaosEnergy", 5)
    TNT1 A 0 A_GiveInventory("Health", 15)
	TNT1 A 0 A_ReFire("AltFire")
	Goto Ready
  Reload:
	TNT1 A 0 A_GiveInventory("ShadowDash")
	Goto Ready
  Spawn:
    CHSM G -1
    Stop
  }
}